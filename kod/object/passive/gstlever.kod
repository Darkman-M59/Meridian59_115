% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GuestLever is PassiveObject

constants:

   include blakston.khd

   GUESTLEVER_RANGE = 1

   GUESTLEVER_UP = 1
   GUESTLEVER_DOWN = 2

resources:

   guestlever_name_rsc = "Der Hebel"
   guestlever_icon_rsc = lever.bgf
   guestlever_desc_rsc = "Obwohl der Hebel ziemlich alt und abgenutzt aussieht, muss er irgendeine Funktion besitzen."


   guestlever_not_in_range = "Der Hebel ist nicht nahe genug, um ihn bedienen zu können."

   guestlever_changed = "Du legst den Hebel um."

   guestlever_stuck = "Der Hebel verweilt in seiner jetzigen Stellung."

classvars:

   vrName = guestlever_name_rsc
   vrIcon = guestlever_icon_rsc
   vrDesc = guestlever_desc_rsc

   viObject_flags = ACTIVATE_YES

properties:

   piState = GUESTLEVER_UP

messages:

   TryActivate(who=$)
   "Return False only if you want user.kod to send its own error message to user."
   {
      if abs(send(who,@getrow)-send(self,@getrow)) > GUESTLEVER_RANGE or
         abs(send(who,@getcol)-send(self,@getcol)) > GUESTLEVER_RANGE
      {
	 Send(who,@MsgSendUser,#message_rsc=guestlever_not_in_range);          
	 return True;
      }

      if piState = GUESTLEVER_UP
      {
	 piState = GUESTLEVER_DOWN;
      }
      else
      {
	 Send(who,@MsgSendUser,#message_rsc=guestlever_stuck);
	 return True;
      }
      
      Send(who,@MsgSendUser,#message_rsc=guestlever_changed);          
      Send(poOwner,@SomethingChanged,#what=self);
     
      Send(poOwner,@LeverChanged,#what=self);
 
      return True;
   }

   IsSwitched()
   {
      return piState = GUESTLEVER_DOWN;
   }

   SetSwitched(switched = True)
   {
      local iNew_state;

      if switched
      {  iNew_state = GUESTLEVER_DOWN; }
      else
      {  iNew_state = GUESTLEVER_UP; }

      if iNew_state <> piState
      {
	 piState = iNew_state;
	 Send(poOwner,@SomethingChanged,#what=self);
      }


      return;
   }

   ReqNewOwner(what = $)
   {
      return False;
   }

   SendAnimation()
   {
      AddPacket(1,ANIMATE_NONE,2,piState);
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

