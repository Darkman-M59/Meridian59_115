% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
WeapAttVamper is WeaponAttribute

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  This weapon steals life away from the target, bit by bit.
%
%  Form is: 
%
%       [ WA_Vamper]
%	
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

constants:

   include blakston.khd   

resources:

   weapattVamper_desc = " Ein unheiliges Glühen scheint alles Leben aus der Luft um die Waffe zu ziehen und ein Hauch von Nols Wahnsinn weht durch deinen Geist."
   Vamper_dm = "Vamper"
   Vamper_fail_use = "%s%s entgleitet Deinen Händen, da Du noch nicht weit genug bist diese Waffe zu benutzen."
   Vamper_fail_use_karma = "Du bist nicht unheilig genug, um diese teuflische Waffe zu führen."
   vamper_worked = "%s%s schaudert, während %s herausgesaugt wird!"
   vamper_worked_target = "%s%s scheint entkräftet, als das Metall der Waffe von %s Dein Fleisch berührt!"

classvars:

   vrDesc = weapattVamper_desc
   vrDesc2 = $

   viItem_Att_Num = WA_Vamper

   viDifficulty = 10
   vrDM_trigger = Vamper_dm

properties:
  
   piValue_modifier = 200   %% modify the object's price by 100%
   piValue_power_modifier = 10
   piEffect_percent = 10

messages:
  
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%%  Effect Functions
%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
      
   ModifyDamage(damage = 0, target = $, wielder = $, lData= $)
   "Wielder has a 1% chance of blinding target with any hit."
   {
      local oSpell;
      if random(1,100) <= piEffect_percent
      {
	 send(wielder,@GainHealth,#amount = bound(damage,0,8));
    	 send(wielder, @MsgSendUser, #message_rsc = vamper_worked,
	    #parm1=send(target,@getcapdef),#parm2=send(target,@GetName),
	    #parm3=send(target,@GetHisHer));
    	 send(target, @MsgSendUser, #message_rsc = vamper_worked_target,
	    #parm1=send(wielder,@getcapdef),#parm2=send(wielder,@GetName),
	    #parm3=send(wielder,@GetHisHer));


      }
      return Damage;
   }


   ItemReqUse(oItem=$,oPlayer=$)
  {
      if send(oPlayer,@GetKarma) > -30
      {
	 send(oPlayer, @MsgSendUser, #message_rsc = Vamper_fail_use_karma);
	 return FALSE;	    
      }
      if send(oItem,@GetProf,#who=oPlayer) < 50      
      {
	 send(oPlayer, @MsgSendUser, #message_rsc = Vamper_fail_use,
	    #parm1=send(oItem,@getdef),#parm2=send(oItem,@GetName));
	 return FALSE;      %% this person needs 50% in the weapon's proficiency to use
      }
      return TRUE;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

