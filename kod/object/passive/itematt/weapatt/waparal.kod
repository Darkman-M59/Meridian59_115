% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
WeapAttParalyzer is WeaponAttribute

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%  This weapon has a chance to blind the opponent every time it does
%     damage.
%
%  Form is: 
%
%       [ WA_Paralyzer]
%	
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

constants:

   include blakston.khd   

resources:

   weapattParalyzer_desc = " Das unheilige Glühen der Aura Alamirs umgibt diese Waffe und Du vermeinst die verlorenen Seelen in Agonie kreischen zu hören. Auf eine seltsame Art und Weise fällt es Dir schwer, Deine Hand wieder zu öffnen, um die Waffe loszulassen."

   Paralyzer_dm = "Lähmer"
   paralyzer_fail_use = "%s%s entgleitet Deinen Händen, da Du noch nicht soweit bist diese Waffe zu benutzen."
   holder_worked = "%s%s scheint im Boden verwurzelt zu sein!"

classvars:

   vrDesc = weapattParalyzer_desc
   vrDesc2 = $

   viItem_Att_Num = WA_Paralyzer

   viDifficulty = 9
   vrDM_trigger = Paralyzer_dm

properties:
  
   % modify the object's price by 100%
   piValue_modifier = 200
   piValue_power_modifier = 10
   piEffect_percent = 15

messages:
  
   %%% Effect Functions
      
   ModifyDamage(damage = 0, target = $, wielder = $, lData= $)
   "Wielder has a chance of paralyzing target with any hit."
   {
      local oSpell;

      if random(1,100) <= piEffect_percent
      {
         oSpell = send(SYS,@FindSpellByNum,#num=SID_HOLD);
	 
         %% no duplicates
         if not Send(target,@IsEnchanted,#what=oSpell)
         {	          
            send(oSpell,@CastSpell,#who=self,#ltargets=[target],#ispellpower=99);
            send(wielder,@MsgSendUser,#message_rsc=holder_worked,
                 #parm1=send(target,@GetCapDef),#parm2=send(target,@GetName));
         }
      }

      return damage;
   }

   ItemReqUse(oItem=$,oPlayer=$)
   {
      % A person needs 50% in the weapon's proficiency to use
      if send(oItem,@GetProf,#who=oPlayer) < 50      
      {
         send(oPlayer, @MsgSendUser, #message_rsc = paralyzer_fail_use,
            #parm1=send(oItem,@getdef),#parm2=send(oItem,@GetName));

         return FALSE;      
      }

      return TRUE;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

