% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Moon is PassiveObject

constants:

   include blakston.khd

resources:

   moon_icon_rsc = moon.bgf
   moon_name_rsc = "Der Mond"
   moon_desc_rsc = "Der Mond glänzt am Himmel, ein majestätischer Himmelskörper aus grauem Fels. Manche Sagen berichten, dass der Mond einst größer war, aber das hat wahrscheinlich genauso viel Wahrheitsgehalt wie die Geschichten von Froschmännern und blauen Drachen."




classvars:

   vrName = moon_name_rsc
   vrIcon = moon_icon_rsc
   vrDesc = moon_desc_rsc

properties:

   piAngle = 0
   piHeight = 0

messages:

   Constructor()
   {
      Send(self,@NewGameHour);
      propagate;
   }

   NewGameHour()
   {
      local i,iHour,iDay;

      iHour = Send(SYS,@GetHour);
      iDay = Send(SYS,@GetDay);

      % east = 0, west = 2048
      piAngle = ((iHour+5) mod 24)*130 + 200*(iDay mod 20);
      if iHour > 12
      {
	 iHour = iHour - 24;
      }
      piHeight = 400 - Abs(300-iDay) - 6*(iHour - 1)*(iHour - 1);
      if piHeight < -200
      {
	 piHeight = -200;
      }
      for i in Send(SYS,@GetUsersLoggedOn)
      {
	 Send(i,@ToCliChangeBackgroundOverlay,#what=self);
      }

      return;
   }

   AddBackgroundObject(who = $)
   "Called by user upon logon"
   {
      Send(who,@ToCliAddBackgroundOverlay,#what=self);
      return;
   }

      
   GetBackgroundOverlayAngle()
   {
      return piAngle;
   }
   
   GetBackgroundOverlayHeight()
   {
      return piHeight;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

