% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GuildSetPassword is GuildCommand

constants:

   include blakston.khd
   include protocol.khd

resources:

   guildsetpassword_name_rsc = "Passwort festlegen"
   guildsetpassword_icon_rsc = light.bgf

   guildsetpassword_lieutenant = "Ändert das Passwort einer Gildenhalle."

   setpassword_not_guilded = "Du bist kein Gildenmitglied und hast kein Passwort zu ändern!"
   setpassword_cant_setpassword = "Nur Gildenführer können das Gildenpasswort ändern!"
   setpassword_no_hall = "Du kannst kein Passwort für Deine Gildenhalle festlegen, wenn Du keine Gildenhalle hast!"

classvars:

   vrName = guildsetpassword_name_rsc
   vrIcon = guildsetpassword_icon_rsc
   vrDesc = guildsetpassword_lieutenant

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viGuildcommand_num = GCID_SET_PASSWORD
   virank_needed = RANK_MASTER

properties:

messages:

   ResetCommand(who=$)
   {
     local oGuild;

     oGuild = send(who,@getguild);
     if send(oGuild,@getguildhall) = $
        {
           send(who,@removeguildcommand,#command_num=viGuildCOmmand_num);
           return;
        }
     propagate;
   }

   DoCommand(who=$, data = $)
   "Change the password of the target player to a new password!"  
   {
      local oGuild, oHall;

      oGuild = send(who,@getguild);

      if oGuild = $
        {
          send(who,@msgsenduser,#message_rsc=setpassword_not_guilded);
          return FALSE;
        }

      if send(oGUild,@getrank,#who=who) < RANK_MASTER
        {
          send(who,@msgsenduser,#message_rsc=setpassword_cant_setpassword);
          return FALSE;
        }

      oHall = send(oGuild,@getguildhall);
      if oHall = $
        {
          send(who,@msgsenduser,#message_rsc=setpassword_no_hall);
          return FALSE;          
        }

      send(oGuild,@changePassword,#password=data);

      propagate;
   }
  
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

