% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GuildExile is GuildCommand

constants:

   include blakston.khd
   include protocol.khd

resources:

   guildexile_name_rsc = "Verbannung"
   guildexile_icon_rsc = light.bgf
   guildexile_desc_Rsc = "So kannst Du jemanden aus Deiner Gilde werfen. Dieser Vorgang bleibt ohne Auswirkungen auf andere Gilden oder Mitglieder höheren Rangs."

   exile_non_user = "%s%s scheint von Deiner kaltherzigen Abweisung keineswegs verletzt zu sein."
   exile_not_member = "%s%s ist kein Mitglied von %s%q!"

   exile_cant_self = "Du kannst Dich nicht selbst verbannen - nimm stattdessen den Befehl ABTRETEN."
   exile_cant_self_gm = "%s muss erst die Position in der Gilde aufgeben, um sie zu verlassen."
   exile_cant_too_high = "Du kannst niemanden verbannen, außer er hat einen niedrigeren Rang als Du selbst."

   exile_to_exiled = "~B%s hat Dich aus den Rängen der Gilde %s%q verwiesen!"
   exile_to_others = "~B%s hat %q aus den Rängen der Gilde %s%q verwiesen!"

   guildexile_necro_nak = "~BJedes Mitglied des Ordens ist durch einen Blutsschwur gebunden. Der einzige Weg, auszutreten, ist der Tod."


classvars:

   vrName = guildexile_name_rsc
   vrIcon = guildexile_icon_rsc
   vrDesc = guildexile_desc_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viGuildcommand_num = GCID_EXILE
   viRank_needed = RANK_LIEUTENANT

   viNecro_enabled = FALSE

properties:

messages:

   GetNumSpellTargets()
   {
      return 1;
   }

   DoCommand(who=$, otarget = $)
   "Kicks the target out of the guild!"
   {
      local yourGuild,myguild,i,lmembers;

      if not isClass(oTarget,&player)
        {
          send(who,@msgsenduser,#message_rsc=exile_non_user,
            #parm1 = send(oTarget,@getcapdef), #parm2 = send(oTarget,@getname));
          return FALSE;
        }

      yourGuild= send(oTarget,@getguild);
      myGuild = send(who, @getguild);

      if yourGuild <> myGuild or yourGuild = $
        {
          send(who,@msgsenduser,#message_rsc=exile_not_member,
              #parm1=send(oTarget,@getdef),
			  #parm2=send(oTarget,@getname),
			  #parm3=send(myGuild,@getdef),
              #parm4=send(myGuild,@getname));
          return FALSE;
        }

      if oTarget=who
        {
          if send(myGuild,@getrank,#who=who) = RANK_MASTER
            { 
              send(who,@msgsenduser,#message_rsc=exile_cant_self_GM,
                 #parm1 = send(myGuild,@getrankname,#rank=RANK_MASTER,#sex=send(who,@getgender)));
            }
          else
            {
              send(who,@msgsenduser,#message_rsc=exile_cant_self);
            }
          return FALSE;
        }

      if send(myGuild,@getrank,#who=who) <= send(myGuild,@getrank,#who=oTarget)
        {
           send(who,@msgsenduser,#message_rsc=exile_cant_too_high);
           return FALSE;
        }

      if isClass(myGuild,&NecromancerGuild) {
         Send(who,@MsgSendUser,#message_rsc=guildexile_necro_nak );
         return FALSE;
      }

      send(myGuild,@removefromguild,#who=oTarget);

      send(oTarget,@msgsenduser,#message_rsc=exile_to_exiled,
         #parm1=send(who,@gettruename),
		 #parm2=send(myguild,@getdef),
         #parm3=send(myguild,@gettruename));

      for i in send(myGuild,@getmemberlist)
        {
          send(first(i),@msgsenduser,#message_rsc=exile_to_others,
            #parm1=send(who,@gettruename),#type2=STRING_RESOURCE,#parm3=send(myguild,@getdef),
            #parm4=send(myguild,@getname),#parm2=send(oTarget,@gettruename));
        }

      propagate;
   }
  
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

