% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
GuildForgeAlliance is GuildCommand

constants:

   include blakston.khd
   include protocol.khd

resources:

   guildforgealliance_name_rsc = "Bündnis schließen"
   guildforgealliance_icon_rsc = light.bgf
   guildforgealliance_desc_text_rsc = "Um sich mit anderen Gilden zusammen zu tun. Wichtigste Vorteile eines Bündnisses - das Teilen der Gildenhallen."

   guildforgealliance_to_inductor = "Du bietest %s einen Vertrag zwischen Deiner Gilde und der Gilde %s%s an."
   guildforgealliance_to_inductee = "%s bietet Dir einen Vetrag zwischen Deiner Gilde und der Gilde %s%s an!"
   guildforgealliance_already_your_guild = "%s ist ein Mitglied Deiner Gilde!"

   ally_not_user = "Du kämst Dir ziemlich blöd dabei vor, mit der Gilde %s%q in den Kampf zu ziehen!"
   ally_cant_ally_self = "Du willst eine Bündniss mit Dir selbst schließen? Sehr gut, Du nimmst an."
   ally_only_one_treaty = "Du kannt nicht mer als einen Vertrag gleichzeitig schließen."

   ally_on_enemy_list = "Du kannst Dich nicht mit der Gilde %s%q verbünden, bevor Ihr nicht die Feindseligkeiten einstellt."
   ally_on_ally_list = "Die grimmigen Krieger der Gilde %s%q sind bereits Eure Verbündeten!"

   ally_too_low = "%s ist kein hochrangiger Gildenvertreter und kann deshalb nicht mit Dir über eine Bündnis verhandeln."

classvars:

   vrName = guildforgealliance_name_rsc
   vrIcon = guildforgealliance_icon_rsc
   vrDesc = guildforgealliance_desc_text_rsc

   viIndefinite = ARTICLE_NONE
   viDefinite = ARTICLE_NONE

   viGuildcommand_num = GCID_FORGE_ALLIANCE
   viShow_enchantment_icon = ENCHANTMENT_SHOW_ALL
   viRank_needed = RANK_LIEUTENANT

properties:

messages:

   GetNumSpellTargets()
   {
      return 1;
   }

   DoCommand(who=$, otarget = $)
   "Create invitation and give it to the target."

   {
      local oGuild, ownguild, rank, i;


      %debug("reaching gcally.kod");
      oGuild= oTarget;
      ownguild = send(who, @getguild);

      if oGuild = ownguild
        {
           send(who,@msgsenduser,#message_rsc = guildforgealliance_already_your_guild);
           return FALSE;
        }
      for i in send(ownguild,@getenemylist)
        {
           if i = oGuild
             {
                send(who,@msgsenduser,#message_rsc = ally_on_enemy_list,
                  #parm1=send(ownguild,@getdef),#parm2=send(ownguild,@getname));
                return FALSE;
             }
        }
      for i in send(ownGuild,@getallylist)
        {
           if i = oguild
             {
                send(who,@msgsenduser,#message_rsc = ally_on_ally_list,
                  #parm1=send(oguild,@getcapdef),#parm2=send(oguild,@getname));
                return FALSE;
             }
        }

      if send(ownguild,@canally,#otherguild=oGuild,#rep=who)
        {
           send(ownguild,@newally,#otherguild=oGuild);
        }
      propagate;
   }
  
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

