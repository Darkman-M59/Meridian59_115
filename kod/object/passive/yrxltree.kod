% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
YrxlTree is PassiveObject

constants:

   include blakston.khd

   SAP_TIME_MIN = 60000*30  % 30 - 90 minutes
   SAP_TIME_MAX = 60000*90

resources:

   yrxltree_name_rsc = "Der Yrxl-Baum"
   yrxltree_desc_rsc = "Dies ist der letzte von vielen Yrxl-Bäumen, der das gedankenlose Abzapfen des Harzes dieses Baumes wegen seiner magischen Eigenschaften überlebt hat. Es ist ein ungeschriebenes Gesetz, dass dieser Baum in Ruhe gelassen und mit höchstem Respekt behandelt wird."

   yrxltree_icon_rsc = yrxltree.bgf

classvars:

   vrName = yrxltree_name_rsc
   vrIcon = yrxltree_icon_rsc
   vrDesc = yrxltree_desc_rsc

properties:

   piBottom = TREE_OAK
   ptSap = $

messages:
   
   constructor(bottom = TREE_OAK,top = $)
   {
   ptSap = CreateTimer(self,@DropSap,Random(SAP_TIME_MIN,SAP_TIME_MAX));
   propagate;
   }

   CheckUserNear()
   {
   local oRoom, each_obj, i;

   oRoom = send(self,@getowner);
   for i in Send(oRoom,@GetHolderActive)
      {
      each_obj = Send(oRoom,@HolderExtractObject,#data=i);
      if isclass(each_obj,&player)
         {
         if (send(each_obj,@SquareddistanceTo,#what=self) < 100)
            { return TRUE; }
         }
      }
   return FALSE;
   } 


   DropSap()
   {
   local oRoom;
   oRoom = Send(self,@getowner);
   if not send(self,@CheckUserNear) and
      not send(oRoom,@PossessesA,#class=&yrxlsap)
      {
      Send(oRoom,@NewHold,#what=Create(&yrxlsap,#Gathered=FALSE),
         #new_row=send(self,@getrow) + 1,
         #new_col=send(self,@getcol) + 1);
      }
   ptSap = CreateTimer(self,@DropSap,Random(SAP_TIME_MIN,SAP_TIME_MAX));
   return;
   }

   Delete()
   {
   if ptSap <> $
      {
      deletetimer(ptsap);
      ptSap = $;
      }
   propagate;
   }

   GetObjectFlags()
   {
      return LOOK_NO;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

