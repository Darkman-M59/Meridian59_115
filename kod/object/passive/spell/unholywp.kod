% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
UnholyWeapon is Spell

%% Apr 20, 1997 DSchubert
%% Duration is based on spell power (1-25 hours).

constants:
   include blakston.khd

resources:

   unholyweapon_cast_rsc = "%s ist nun Qor geweiht."
   unholyweapon_no_can = "%s%s kann Qor nicht geweiht werden."
   unholyweapon_fails = "%s zeigt keine Veränderungen durch den Zauber."
   unholyweapon_already_done = "Diese Waffe ist bereits Qor geweiht."


   unholyweapon_name_rsc = "Unheilige Waffe"
   unholyweapon_icon_rsc = iunholyw.bgf
   unholyweapon_desc_rsc = "Weiht Deine Waffe Qor und steigert ihre Wirksamkeit gegen das Heilige. Der Zauber benötigt eine gewisse Zeitspanne an Konzentration, Feenflügel und Orkzähne."




   unholyweapon_spell_intro = "Qor Zirkel 2: Weiht Deine Waffe Qor und steigert ihre Wirksamkeit gegen das Heilige."

   unholyweapon_lable_rsc = "Dunkelklinge."

classvars:

   vrName = unholyweapon_name_rsc
   vrIcon = unholyweapon_icon_rsc
   vrDesc = unholyweapon_desc_rsc

   vrEnchantment_type = unholyweapon_lable_rsc

   vrSpell_intro =unholyweapon_spell_intro
   viSpell_num = SID_UNHOLY_WEAPON
   viSchool = SS_QOR

   viSpell_level = 2
   viMana = 17
   viSpellExertion = 20
   viCast_time = 30000

properties:

messages:

   ResetReagents()
   {
      % separate message so can change, then set from admin mode

      plReagents = $;
      plReagents = Cons([&FairyWing,3],plReagents);
      plReagents = Cons([&OrcTooth,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      % Tell client how many targets this spell needs
      return 1;
   }

   IsTargetInRange(who = $,target = $)
   {
      return who = Send(target, @GetOwner) 
      OR send(who,@getOwner) = send(target,@getowner);  
   }

   CanPayCosts(who = $, lTargets = $, iSpellPower = 0)
   {
      local oWeapon;
      oWeapon = first(lTargets);
      if oWeapon = $ or not isClass(oWeapon,&weapon)
      {
	 Send(who, @MsgSendUser, #message_rsc=unholyweapon_no_can,
	    #parm1=send(oWeapon,@getdef),#parm2=send(oWeapon,@getname));
	 return False;
      }
      if Send(oWeapon,@CheckSpellFlag,#flag=ATCK_SPELL_UNHOLY)
      {
	 Send(who, @MsgSendUser, #message_rsc=unholyweapon_already_done);
	 return False;
      }
      propagate;   % Check other things higher up
   }

   CastSpell(who = $, lTargets = $, iSpellPower = 0)
   {
      local oWeapon, oWeapAtt, iDuration;

      oWeapon = first(lTargets);
      if oWeapon = $ or not isClass(oWeapon,&weapon)
      {
	 Send(who, @MsgSendUser, #message_rsc=unholyweapon_no_can,
	    #parm1=send(oWeapon,@getdef),#parm2=send(oWeapon,@getname));
	 return False;
      }

      if not Send(oWeapon,@CanEnchant,#oSpell = self)
      {
         Send(who, @MsgSendUser, #message_rsc=spell_resists,
             #parm1=send(oWeapon,@getCapdef),#parm2=send(oWeapon,@getname));
	 return;
      }
      
      if not Send(self,@IsTargetInRange,#who=who,#target=oWeapon)
      {
	 Send(who, @MsgSendUser,#message_rsc=spell_out_of_range, 
	    #parm1=Send(oWeapon,@GetCapDef),#parm2=Send(oWeapon,@GetName));
	 return False;
      }     

      oWeapAtt = send(SYS,@finditemattbynum,#num=WA_ATTACKSPELLTYPE);	  

      send(oWeapAtt,@AddToItem,#oItem=oWeapon, #random_Gen=FALSE,#state1=ATCK_SPELL_UNHOLY, 
	 #timer_duration = send(self,@getDuration,#iSpellPower=iSpellPower),#state3=send(self,@GetMakerInfo,#who=who));

      Send(who, @MsgSendUser, #message_rsc=unholyweapon_cast_rsc,
	 #parm1=Send(oWeapon,@GetName));

	  propagate;
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      iDuration = random(1,4) + (iSpellPower/3);
      %% ~( 1 - 37 )
      %%Convert to hours:
      iDuration = iDuration * 1000 * 60 * 60;
      return iDuration;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

