% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Relay is Spell

constants:

   include blakston.khd

   MAX_TOTAL_AMOUNT = 150      % Max target can reach after getting benefit.

resources:

   Relay_cast_rsc = "Du fühlst, wie Dich die Ausdauer einer anderen Person durchströmt."
   Relay_caster_msg_rsc = "Du überträgst einen Teil Deiner Ausdauer auf %s%s."

   Relay_name_rsc = "Auffrischen"
   Relay_icon_rsc = irelay.bgf
   Relay_desc_rsc = "Die Macht Kraanans erlaubt es Dir, einen Teil Deiner Ausdauer an eine andere Person zu übertragen. Der Zauber benötigt genießbare Pilze."



   Relay_unnecessary_rsc = "%s%s kann keine weitere Ausdauer von Dir aufnehmen."
   Relay_toolow_rsc = "Du hast nicht genug Ausdauer um sie an Andere zu übertragen!"
   Relay_notonself_rsc = "Du kannst Dir nicht selbst Ausdauer übertragen."

   Relay_spell_intro = "Kraanan Zirkel 1: Gibt einen Teil der Ausdauer des Zaubernden einer anderen Person."

   Relay_sound = krelay.wav

classvars:

   vrName = Relay_name_rsc
   vrIcon = Relay_icon_rsc
   vrDesc = Relay_desc_rsc
   vrSucceed_wav = Relay_sound

   vrSpell_intro = Relay_spell_intro

   viSpell_num = SID_RELAY
   viSchool = SS_KRAANAN
   viSpell_level = 1
   viMana = 5
   viSpellExertion = 0
   viChance_To_Increase = 20
  
   viFlash = FLASH_GOOD

properties:

messages:

   ResetReagents()
   {
      % separate message so can change, then set from admin mode

      plReagents = $;
      plReagents = Cons([&Snack,1],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 1;
   }

   CanPayCosts(who = $, lTargets = $)
   {
      local target, i;
      
      % Can cast spell if the 1 target item is a user
      if Length(lTargets) <> 1
      {
         return FALSE;
      }

      target = First(lTargets);
      if NOT IsClass(target, &User)
      {
         Send(who,@MsgSendUser,#message_rsc=spell_bad_target, 
              #parm1=vrName,#parm2=Send(target,@GetDef),#parm3=Send(target,@GetName));

         return FALSE;
      }

      if target = who
      {
         Send(who,@MsgSendUser,#message_rsc=Relay_notonself_rsc);

         return FALSE;
      }

      % no giving if less than 15 vigor
      if Send(who,@GetVigor) <= 15
      {
         Send(who,@MsgSendUser,#message_rsc=Relay_toolow_rsc);

         return FALSE;
      }

      % max of 150 or so.
      if Send(target,@GetVigor) >= MAX_TOTAL_AMOUNT 
      {
         Send(who,@MsgSendUser,#message_rsc=Relay_unnecessary_rsc,
              #parm1=Send(target,@GetDef),#parm2=Send(target,@GetName));

         return FALSE;
      }

      propagate;   % Check other things higher up
   }

   CastSpell(who = $, lTargets = $, iSpellPower=0)
   {
      local oTarget, iEpAmount;

      oTarget = First(lTargets);

      % Amount transferred is straight-up spell power
      iEpAmount = iSpellPower;
      
      if iEpAmount > (Send(who,@GetVigor) - 10)
      {
         iEpAmount = Send(who,@GetVigor) - 10;
      }

      if (iEpAmount + Send(oTarget,@GetVigor)) > MAX_TOTAL_AMOUNT
      {
         iEpAmount = MAX_TOTAL_AMOUNT - Send(oTarget,@GetVigor);
      }

      % Convert vigor to Exertion
      iEpAmount = iEpAmount * 10000;
      Send(who,@MsgSendUser,#message_rsc=Relay_caster_msg_rsc,
           #parm1=Send(oTarget,@GetDef),#parm2=Send(oTarget,@GetName));

      % Positive means we decrease Vigor
      Send(who,@AddExertion,#amount=iEpAmount);
      Send(oTarget,@MsgSendUser,#message_rsc=Relay_cast_rsc);

      % Negative means we increase vigor, and give a 25% bonus.
      iEpAmount = (-5 * iEpAmount)/4;
      Send(oTarget,@AddExertion,#amount=iEpAmount);

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

