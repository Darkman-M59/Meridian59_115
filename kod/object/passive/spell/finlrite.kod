% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
FinalRites is Spell

constants:

   include blakston.khd

resources:

   FinalRites_name_rsc = "Letztes Sakrament"
   FinalRites_icon_rsc = ifinrite.bgf
   FinalRites_desc_rsc = "Diese langatmige, offizielle Zeremonie wird auf Beerdigungen abgehalten um die Seelen der Verstorbenen möglichst sanft in die Unterwelt zu geleiten. Sie wird auch angewendet, um Untote in der Nähe in die Gräber zurückzutreiben, wo sie hingehören. Der Zauber benötigt Smaragde und Holunderbeeren, welche während der Zeremonie symbolisch zu Staub zermalmt und in der Luft verteilt werden."







   
   FinalRites_success = "Du zermahlst die Edelsteine zu feinem Pulver und mit einem Murmeln wirfst Du die Handvoll Staub himmelwärts."


   FinalRites_success_others = "Mit einem feierlichen Gemurmel wirft %s%s die Handvoll Staub himmelwärts."


   FinalRites_hurt = "%s%s kreischt vor Pein, als der Staub wie heiliger Regen niedergeht."

   FinalRites_hurt_necro = "%s%s wirft eine Handvoll Staub in die Luft und ein kochender Regen geht auf Dich hernieder."



   FinalRites_sound = sfrites.wav

classvars:

   vrName = FinalRites_name_rsc
   vrIcon = FinalRites_icon_rsc
   vrDesc = FinalRites_desc_rsc

   viSpell_num = SID_FINAL_RITES
   viSchool = SS_SHALILLE
   viSpell_level = 5
   viMana = 20
   viSpellExertion = 15

   % This will disallow people from trucing, and then killing undead.
   viHarmful = TRUE   

   viCast_time = 10000
   viChance_To_Increase = 20

   vrSucceed_wav = FinalRites_sound

properties:

   viOutlaw = TRUE

messages:

   Constructor()
   {
      if NOT Send(SYS,@IsPKAllowed)
      {
         viOutlaw = FALSE;
      }

      propagate;
   }

   ResetReagents()
   {
      plReagents = Cons([&Emerald,2],plReagents);
      plReagents = Cons([&Elderberry,5],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   GetTargets(who=$,lTargets=$)
   "This returns a list of undead targets."
   {
      local oRoom, i, each_obj, lFinalTargets;

      lFinalTargets = $;

      oRoom = Send(who,@GetOwner);
      for i in Send(oRoom,@GetHolderActive)
      {
         each_obj = Send(oRoom,@HolderExtractObject,#data=i);
         if IsClass(each_obj,&Battler)
            AND Send(each_obj,@IsUndead)
         {
            lFinalTargets = Cons(each_obj,lFinalTargets);
         }
      }

      return lFinalTargets;
   }

   CastSpell(who=$, lTargets = $, iSpellPower=0)
   {
      local oUndead,iDamage,oRoom;
      
      oRoom = Send(who,@GetOwner);
      Send(who,@MsgSendUser,#message_rsc=FinalRites_success);

      Send(oRoom,@SomeoneSaid,#type=SAY_MESSAGE,#what=self,
           #string=FinalRites_success_others,
           #parm1=Send(who,@GetDef),#parm2=Send(who,@GetName));

      for oUndead in lTargets
      {
         if isClass(oUndead,&Player)
         {
            Send(oUndead,@MsgSendUser,#message_rsc=FinalRites_hurt_necro,
                  #parm1=Send(who,@GetCapDef),#parm2=Send(who,@GetName));
         }
            
         % Do damage to them.
         iDamage = random(1,5) + (iSpellpower/20)
                   + (2*((Send(who,@GetKarma,#detect=TRUE)
                         - Send(oUndead,@GetKarma,#detect=TRUE))/40));
         if Send(oUndead,@AssessDamage,#what=who,#damage=iDamage,
                 #aspell=ATCK_SPELL_HOLY) = $
         {
            Send(who,@KilledSomething,#what=oUndead,#use_weapon=self);
         }

         Send(oRoom,@SomethingAttacked,#what=who,#victim=oUndead,#use_weapon=self);
      }
      
      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

