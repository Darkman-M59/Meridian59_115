% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
FreeAction is PersonalEnchantment

constants:

   include blakston.khd

   % What percent of spellpower should we take in order to give the player
   %  a chance to completely resist hold?
   SPELL_RESIST_PERCENT = 15

resources:

   FreeAction_name_rsc = "Fairer Kampf"
   FreeAction_icon_rsc = ifreeact.bgf
   FreeAction_desc_rsc = "Kraanans Liebe zur Schlacht hilft Dir dabei, lähmende Magie abzuwehren und ermöglicht Dir somit einen gerechteren Kampf. Dieser Zauber benötigt lila und rote Pilze."



   
   FreeAction_on_rsc = "Du fühlst, wie Dich eine Energiewelle durchströmt und Deine Bewegungen flüssiger erscheinen läßt."

   FreeAction_off_rsc = "Das seltsame Gefühl der Freiheit verlässt Dich wieder."
   FreeAction_already_enchanted_rsc = "Du bist bereits auf einen fairen Kampf vorbereitet."
   FreeAction_succeed_rsc = "Du ermöglichst %s%s einen fairen Kampf."

   FreeAction_resist = "Du kannst die Kräfte Kraanans spüren, die Dich befreien."
   FreeAction_evade = "Du bemerkst einen Versuch, Deine Bewegungsfreiheit einzuschränken."
   FreeAction_resist_caster = "Du spürst eine Kraft, die gegen Deine Bewegungsfreiheit ankämpft."

   FreeAction_evade_caster = "Kraanan's Magie schützt Dich davor, von anderen daran gehindert zu werden das Schlachtfeld zu verlassen."


   FreeAction_sound = kfreeact.wav

classvars:

   vrName = FreeAction_name_rsc
   vrIcon = FreeAction_icon_rsc
   vrDesc = FreeAction_desc_rsc
   vrSucceed_wav = FreeAction_sound


   vrAlreadyEnchanted = FreeAction_already_enchanted_rsc
   vrEnchantment_On = FreeAction_On_rsc
   vrEnchantment_Off = FreeAction_Off_rsc
   vrSucceed = FreeAction_succeed_rsc

   viSpell_num = SID_FREE_ACTION
   viSchool = SS_KRAANAN
   viSpell_level = 3
   viMana = 10
   viSpellExertion = 5

   viCast_time = 2000
   viChance_To_Increase = 15

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&RedMushroom,1],plReagents);
      plReagents = Cons([&PurpleMushroom,1],plReagents);

      return;
   }

   GetStateValue(iSpellpower=$)
   {
      local iFactor;

      iFactor = bound(iSpellPower/2+5,10,50);      

      return iFactor;
   }
  
   GetDuration(iSpellpower=0)
   {
      local iDuration;

      % 3 - 8 minutes max, converted to ms.
      iDuration = 180 + (3 * iSpellPower);   
      iDuration = iDuration * 1000;
      
      return random(iDuration/2,iDuration);
   }

   DoFreeAction(oHoldCaster=$, oTarget=$, iDuration=0)
   "Calculates adjustment if hold duration.  Returns $ if resisted completely."
   {
      local iNewDuration, oFreeActionStrength;

      oFreeActionStrength = Send(oTarget,@GetEnchantedState,#what=self);

      % Check if they repel hold outright 1 - 15% chance.
      if Random(0,100) <= ((oFreeActionStrength * SPELL_RESIST_PERCENT) / 100)
      {
         Send(oTarget,@MsgSendUser,#message_rsc=FreeAction_evade);
         if IsClass(oHoldCaster,&Player)
         {
            Send(oHoldCaster,@MsgSendUser,#message_rsc=FreeAction_evade_caster);
         }
         
         return $;
      }

      iNewDuration = iDuration - (iDuration * oFreeActionStrength)/150;
      Post(oTarget,@MsgSendUser,#message_rsc=FreeAction_resist);
      
      if IsClass(oHoldCaster,&Player)
      {
         Post(oHoldCaster,@MsgSendUser,#message_rsc=FreeAction_resist_caster);
      }

      return iNewDuration;
   }

   OfferToNewCharacters()
   {
      % It's a PK-types spell.  Don't offer to newbies.
      return FALSE;
   }

   GetPotionClass()
   {
      return &FreeActionPotion;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

