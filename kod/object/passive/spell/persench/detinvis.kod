% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DetectInvisible is PersonalEnchantment

constants:

   include blakston.khd

resources:

   DetectInvisible_name_rsc = "Unsichtbares entdecken"
   DetectInvisible_icon_rsc = idetinvi.bgf
   DetectInvisible_desc_rsc = "Der Zauber lässt Dich Gestalten erkennen, die anderen verborgen bleiben. Benötigt Phiolen mit Solagh."

   DetectInvisible_on_rsc = "Du kannst fühlen, wie Deine Augen ihren Focus verschieben."
   DetectInvisible_off_rsc = "Deine Sicht kehrt zur vorherigen Perspektive zurück."
   DetectInvisible_already_enchanted_rsc = "Du kannst bereits das Unsichtbare sehen."
   DetectInvisible_Success_rsc = "%s%s's Augen leuchten kurz auf."

classvars:

   vrName = DetectInvisible_name_rsc
   vrIcon = DetectInvisible_icon_rsc
   vrDesc = DetectInvisible_desc_rsc

   vrAlreadyEnchanted = DetectInvisible_already_enchanted_rsc
   vrEnchantment_On = DetectInvisible_On_rsc
   vrEnchantment_Off = DetectInvisible_Off_rsc
   vrSuccess = DetectInvisible_Success_rsc

   viSpell_num = SID_DETECT_INVISIBLE
   viSchool = SS_KRAANAN
   viSpell_level = 3
   viMana = 15
   viSpellExertion = 5

   viCast_time = 3000
   viChance_To_Increase = 15


properties:

messages:

   ResetReagents()
   {
      plReagents = [ [&Solagh, 2] ];

      return;
   }

   CastSpell(who = $,iSpellPower=0)
   {
      %% post this so it's after the flag being applied.
      post(who,@ToCliRoomContents);
      
      propagate;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetPlayerFlag,#flag=PFLAG2_DETECT_INVIS,#flagset=2,#value=True);
      
      return;
   }
   
   GetDuration(iSpellpower = 0)
   {
      local iDuration;
      iDuration = 2400 * (100 + iSpellPower);   % 4 - 8 minutes max,
      
      return random(iDuration/2,iDuration);     % randomly choose between max and half-max
   }
   
   EndEnchantment(who = $, report = TRUE, state = 0)
   {
      if send(who,@IsLoggedOn)
      {
	      %% must post this so it's after the flag being removed.
	      post(who,@ToCliRoomContents);
      }
     
      propagate;
   }

   GetPotionClass()
   {
      RETURN &DetectInvisibilityPotion;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

