% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%S%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
ArmorOfGort is PersonalEnchantment

constants:

   include blakston.khd

resources:

   ArmorOfGort_name_rsc = "Gorts Panzer"
   ArmorOfGort_icon_rsc = igort.bgf
   ArmorOfGort_desc_rsc = "Umgibt den Zaubernden mit einem mächtigen Schutz, der schweren Schaden durch harte Schläge abwehrt. Der Zauber benötigt Drachenschuppen."



   
   ArmorOfGort_already_enchanted_rsc = "Du hast den Gorts Panzer-Zauber bereits ausgeführt."

   ArmorOfGort_on_rsc = "Deine Haut fühlt sich straff an, als sich Gorts Panzer um Dich legt."
   ArmorOfGort_off_rsc = "Gorts Panzer verflüchtigt sich."
   ArmorOfGort_success_rsc = "%s%s's Haut strafft sich durch Gorts Panzer."

   ArmorOfGort_spell_intro = "Kraanan Zirkel 5: Begrenzt den Schaden, der angerichtet wird."

classvars:

   vrName = ArmorOfGort_name_rsc
   vrIcon = ArmorOfGort_icon_rsc
   vrDesc = ArmorOfGort_desc_rsc

   vrAlreadyEnchanted = ArmorOfGort_already_enchanted_rsc
   vrEnchantment_On = ArmorOfGort_On_rsc
   vrEnchantment_Off = ArmorOfGort_Off_rsc
   vrSuccess = ArmorOfGort_Success_rsc

   viSpell_num = SID_ARMOR_OF_GORT

   vrSpell_intro = ArmorOfGort_spell_intro

   viMana = 10

   viSpell_level = 5
   viSchool = SS_KRAANAN

   viChance_To_Increase = 20
   viFlash = FLASH_GOOD_SELF

properties:    

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&BlueDragonScale,2],plReagents);

      return;
   }

   GetDuration(iSpellPower = 0)
   {
      local iDuration; 

      iDuration = iSpellPower/8 + 1;
      % Convert to minutes
      iDuration = iDuration * 60 * 1000;
      
      return iDuration;
   }

   GetStateValue(who = $, iSpellPower = 0, Target = $)      
   {
      Send(Target,@AddDefenseModifier,#what=self);
       
      return iSpellPower;
   }

   ModifyDefenseDamage(who = $, what = $, damage = $, atype = 0, aspell = 0)
   {
      local iSpellPower, iAbsorbed, iMax;

      iSpellPower = Send(who,@GetEnchantedState,#what=self);
      iMax = 15;
      iAbsorbed = random(0,iSpellPower/25);
      iAbsorbed = bound(iAbsorbed,1,4);

      if aspell <> 0
      {
         % Reduce damage if part of the damage is spell
         if atype <> 0
         {
            % Only 1/2 of the damage reduction if we're doing both weapon
            %  and spell damage types.
            iAbsorbed = iAbsorbed / 2;
            iMax = 20;
         }
         else
         {
            % If we're doing pure spell damage, we get no reduction.
            iAbsorbed = 0;
            iMax = 25;
         }
      }

      return bound(damage-iAbsorbed,1,iMax);
   }

   EndEnchantment(who = $, report = TRUE, state = 0)
   {
      Send(who,@RemoveDefenseModifier,#what=self);

      propagate;
   }
      

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

