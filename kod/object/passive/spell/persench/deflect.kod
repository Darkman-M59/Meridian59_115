% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%S%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Deflect is PersonalEnchantment

constants:

   include blakston.khd

resources:

   Deflect_name_rsc = "Reflektieren"
   Deflect_icon_rsc = imirror.bgf
   Deflect_desc_rsc = "Ruft die Kraft Kraanans um einen Schutzwall um das Ziel zu legen, welcher es vor der Magie Farens schützt. Benötigt Phiolen mit Solagh und Kriipa Klauen.\n"




   
   Deflect_already_enchanted_rsc = "Du bist bereits von einer magischen Barriere umgeben."

   Deflect_on_rsc = "Eine magische Barriere umgibt Dich."
   Deflect_success_rsc = "Du spürst, das %s%s von einer magischen Barriere umgeben ist."
   Deflect_off_rsc = "Deine magische Barriere scheint abzuflauen."

   Deflect_spell_intro = "Kraanan Zirkel 4: Erzeugt eine magische Barriere um das Ziel als Schutz gegen Faren's Magie."



   Deflect_sound = kdeflect.wav

   Deflect_success_to_victim = "Deine magische Barriere wirft %s's Zauber auf ihn selbst zurück!"

   Deflect_success_to_caster = "Eine magische Barriere umgibt %s, Dein Zauber wird auf Dich selbst zurückgeworfen!"


   Deflect_failure_to_victim = "Deine magische Barriere versucht %s's Zauber zurückzuwerfen, ist aber nicht stark genug."



classvars:

   viPersonal_ench = True

   viSpellExertion = 10
   vrName = Deflect_name_rsc
   vrIcon = Deflect_icon_rsc
   vrDesc = Deflect_desc_rsc

   vrAlreadyEnchanted = Deflect_already_enchanted_rsc
   vrEnchantment_On = Deflect_On_rsc
   vrEnchantment_Off = Deflect_Off_rsc
   vrSuccess = Deflect_Success_rsc

   viSpell_num = SID_DEFLECT

   vrSpell_intro = Deflect_spell_intro

   viMana = 20
   viCast_time = 3000
   viSpell_level = 4
   viSchool = SS_KRAANAN

   viChance_To_Increase = 20

   viFlash = FLASH_GOOD_SELF

   vrSucceed_wav = Deflect_sound

properties:
   
messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Solagh,1],plReagents);
      plReagents = Cons([&KriipaClaw,1],plReagents);

      return;
   }

   GetStateValue(who = $,iSpellPower= 0,Target=$)
   {
      local iReflectOdds;

      iReflectOdds = iSpellPower * 3 / 4;

      return bound(iReflectOdds,10,75);
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      % 21-119 seconds
      iDuration = (20 + iSpellPower) * 1000;

      return random(iDuration/2,iDuration);
   }

   TryDeflect(caster = $, victim = $, oSpell = $)
   "Returns TRUE if the spell is deflected."
   {
      local chance;

      if Send(oSpell,@IsHarmful)
         AND Send(oSpell,@GetNumSpellTargets) = 1
         AND IsClass(oSpell,&AttackSpell)
      {
         chance = Send(victim,@GetEnchantedState,#what=self);
         if Random(0,100) < chance
         {
            Send(self,@SendMessageTryDeflectSuccess,#victim=victim,#caster=caster);

            return TRUE;
         }
         
         Send(self,@SendMessageTryDeflectFailure,#victim=victim,#caster=caster);
      }

      return FALSE;
   }

   SendMessageTryDeflectSuccess(victim=$,caster=$)
   {
      Send(victim,@MsgSendUser,#message_rsc=Deflect_success_to_victim,
           #parm1=Send(caster,@GetName));
      Send(caster,@MsgSendUser,#message_rsc=Deflect_success_to_caster,
           #parm1=Send(victim,@GetName));

      return;
   }

   SendMessageTryDeflectFailure(victim=$,caster=$)
   {
      Send(victim,@MsgSendUser,#message_rsc=Deflect_failure_to_victim,
           #parm1=Send(caster,@GetName));

      return;
   }

   GetPotionClass()
   {
      return &DeflectPotion;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

