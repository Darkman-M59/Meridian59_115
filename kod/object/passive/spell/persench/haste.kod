% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Haste is PersonalEnchantment

constants:

   include blakston.khd

resources:

   Haste_name_rsc = "Hast"
   Haste_icon_rsc = ihaste.bgf
   Haste_desc_rsc = "Sorgt dafür, dass Du nicht so schnell ermüdest, wenn Du mit voller Kraft läufst. Der Zauber benötigt genießbare Pilze."


   
   Haste_on_rsc = "Dein Blut pulsiert schneller und befreit Deine Beinmuskulatur mit jedem Herzschlag von der Ermüdung."
   Haste_off_rsc = "Du spürst, dass die Energiephase in Deinem Blut nachlässt, und das Laufen scheint Dich plötzlich wieder mehr zu strapazieren."
   Haste_already_enchanted_rsc = "Du bist schon gegen Müdigkeit resistent."
   Haste_success_rsc = "Du befreist %s%s's Beine von ihrer Ermüdung."

   Haste_spell_intro = "Kraanan Zirkel 2: Hilft Dir, weniger Ausdauer beim Laufen zu verlieren."

   Haste_sound = khaste.wav

classvars:

   vrName = Haste_name_rsc
   vrIcon = Haste_icon_rsc
   vrDesc = Haste_desc_rsc

   vrAlreadyEnchanted = Haste_already_enchanted_rsc
   vrEnchantment_On = Haste_On_rsc
   vrEnchantment_Off = Haste_Off_rsc
   vrSuccess = Haste_Success_rsc

   viSpell_num = SID_HASTE
   viSchool = SS_KRAANAN
   viSpell_level = 2
   viMana = 10
   viSpellExertion = 0

   viCast_time = 2000
   viChance_To_Increase = 15

   vrSpell_intro = Haste_spell_intro
   vrSucceed_wav = Haste_sound

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&Snack,5],plReagents);

      return;
   }

   GetStateValue(iSpellPower=0)
   {
      return iSpellPower;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetPlayerFlag,#flag=PFLAG2_HASTED,#flagset=2,#value=True);
      return;
   }
   
   GetDuration(iSpellpower = 0)
   {
      local iDuration;
      iDuration = 5 + ((15 * iSpellPower) / 100);   % 5 - 20 minutes,
      iDuration = iDuration * 1000 * 60;   % Convert to milliseconds
      
      return random(iDuration/2,iDuration);
   }

   GetPotionClass()
   {
      RETURN &HastePotion;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

