% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Invisibility is PersonalEnchantment

constants:

   include blakston.khd

resources:

   invisibility_name_rsc = "Unsichtbarkeit"
   invisibility_icon_rsc = iinvisib.bgf
   invisibility_desc_rsc = "Qor bedeckt Deinen Körper mit einem schimmernden Feld und macht Dich kurzzeitig unsichtbar. Der Zauber benötigt die Federn eines schwarzen Engels."



   
   invisibility_already_enchanted_rsc = "Du bist bereits unsichtbar."

   invisibility_on = "Ein schimmerndes Feld verdeckt Deinen Körper."
   invisibility_off = "Das schimmernde Feld um Dich löst sich auf und Du wirst wieder sichtbar."


   invisibility_success_rsc = "%s%s verschwindet aus der Sicht."
      

   invisibility_sound = qinvis.wav

classvars:

   viPersonal_ench = TRUE

   vrName = invisibility_name_rsc
   vrIcon = invisibility_icon_rsc
   vrDesc = invisibility_desc_rsc

   vrAlreadyEnchanted = invisibility_already_enchanted_rsc
   vrEnchantment_On = invisibility_on
   vrEnchantment_Off = invisibility_off
   vrSuccess = invisibility_success_rsc

   viSpell_num = SID_INVISIBILITY
   viSchool = SS_QOR
   viSpell_level = 5
   viMana = 12
   viSpellExertion = 8

   vrSucceed_wav = invisibility_sound

   vbCanCastOnOthers = FALSE

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&DarkAngelFeather,1],plReagents);

      return;
   }

   CastSpell(who = $,iSpellPower=0, lTargets = $)
   {
      % If it's only self cast, spoof self as target for following code
      if NOT vbCanCastonOthers
      {
         lTargets = Cons(who, lTargets);
      }

      Send(first(lTargets),@ResetPlayerFlagList);
      Send(first(lTargets),@AddDefenseModifier,#what=self);
      
      propagate;
   }

   SetSpellPlayerFlag(who=$)
   {
      Send(who,@SetPlayerFlag,#flag=PFLAG_INVISIBLE,#value=TRUE);

      return;
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      % 20 - 218 seconds
      iDuration = 20 + (iSpellPower*2);
      iDuration = iDuration * 1000;

      return random(iDuration/2,iDuration);
   }

   GetStateValue(iSpellpower=$)
   {
      return iSpellpower;
   }

   EndEnchantment(who = $, report = TRUE, state = 0)
   {
      send(who,@RemoveDefenseModifier,#what=self);
      
      propagate;
   }

   % Stuff we handle to be a defense modifier.

   ModifyDefensePower(who = $,what = $,defense_power = 0)
   {
      local iSpellpower;

      iSpellpower = send(who,@GetEnchantedState,#what=self);
      
      return (defense_power + 25 + iSpellpower);
   }

   ModifyDefenseDamage(who = $,what = $,damage = $)
   {
      return damage;
   }

   DefendingHit(who = $,what = $)
   {
      return;
   }

   IsIllusion()
   {
      return TRUE;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

