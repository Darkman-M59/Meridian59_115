% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DispelIllusion is Spell

constants:

   include blakston.khd

resources:

   dispelillusion_name_rsc = "Illusionen auflösen"
   dispelillusion_icon_rsc = idispill.bgf
   dispelillusion_desc_rsc = "Vertreibt jegliche Illusion in Deiner Nähe. Mit diesem machtvollen Spruch kann man die neuentdeckte Riija-Magielehre beinahe eindämmen, allerdings nur mit hohem Einsatz von Reagenzien, Zauberkraft und Ausdauer. Man benötigt dazu Augen einer Drachenfliege, ungeschliffene Seraphyme und Phiolen mit Solagh."





   dispelillusion_castermsg_rsc = "Du verdrehst einen Augenblick lang die Augen und alle Vorspiegelungen um Dich herum verschwinden."



   dispelillusion_sound = kdispill.wav

classvars:
   vrName = dispelillusion_name_rsc
   vrIcon = dispelillusion_icon_rsc
   vrDesc = dispelillusion_desc_rsc
  
   vrSucceed_wav = dispelillusion_sound

   viSpell_num = SID_DISPEL_ILLUSION
   viSchool = SS_KRAANAN
   viSpell_level = 4
   viMana = 20
   viSpellExertion = 15

properties:

messages:

   ResetReagents()
   {
      plReagents = $;
      plReagents = Cons([&DragonflyEye,1],plReagents);
      plReagents = Cons([&UncutSeraphym,1],plReagents);
      plReagents = Cons([&Solagh,2],plReagents);

      return;
   }

   GetNumSpellTargets()
   {
      return 0;
   }

   CastSpell(who = $, iSpellPower = 0)
   {
      local oRoom, iChance, bAll;

      oRoom = Send(who,@GetOwner);

      % A percent chance based on spellpower
      if IsClass(who,&Admin)
         AND Send(who,@PlayerIsImmortal)
      {
         iChance = 100;
      }
      else
      {
         iChance = Bound(iSpellPower,10,99);
      }

      % Only attempt to dispell all illusions if the player is unangeled.
      bAll = Send(who,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE);

      if bAll
      {
         Send(oRoom,@DispelIllusionEnchantments,#iChance=iChance);
      }

      Send(oRoom,@DispelIllusions,#what=self,#iChance=iChance,#bAll=bAll);

      Send(who,@MsgSendUser,#message_rsc=dispelillusion_castermsg_rsc);

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

