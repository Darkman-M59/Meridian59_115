% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
EnchantWeapon is Spell

constants:

   include blakston.khd

resources:

   enchantedweapon_cast_rsc = "%s ist jetzt Kraanan geweiht."
   enchantweapon_no_can = "Du darfst %s%s nicht verzaubern."
   enchantweapon_fails = "%s zeigt keine Veränderungen durch den Zauber."
   enchantweapon_already_done = "Diese Waffe wurde schon von Kraanan gesegnet."


   enchantweapon_name_rsc = "Waffe segnen"
   enchantweapon_icon_rsc = ienchant.bgf
   enchantweapon_desc_rsc = "Weiht Deine Waffe Kraanan. Der Zauber benötigt viele Holunderbeeren, einige Orkzähne und Zeit."



   enchantweapon_resists = "%s%s widersteht Deinen Versuchen, den Segen-Zauber anzuwenden."
   enchantweapon_spell_intro = "Kraanan Zirkel 2: Veredelt Deine Waffe mit einem Zauber und verbessert dadurch den Schaden."

   enchantweapon_lable_rsc = "Blutklinge."

classvars:

   vrName = enchantweapon_name_rsc
   vrIcon = enchantweapon_icon_rsc
   vrDesc = enchantweapon_desc_rsc

   vrEnchantment_type = enchantweapon_lable_rsc   

   vrSpell_intro = enchantweapon_spell_intro

   viSpell_num = SID_ENCHANT_WEAPON
   viSchool = SS_KRAANAN

   viSpell_level = 2
   viMana = 17

   viCast_time = 30000
   viSpellExertion = 20

properties:

messages:

   ResetReagents()
   {
      % separate message so can change, then set from admin mode

      plReagents = $;
      plReagents = Cons([&Elderberry,3],plReagents);
      plReagents = Cons([&orctooth,1],plReagents);
		
      return;
   }

   GetNumSpellTargets()
   {
      % Tell client how many targets this spell needs
      return 1;
   }

   IsTargetInRange(who = $,target = $)
   {
      return who = Send(target, @GetOwner) 
      OR send(who,@getOwner) = send(target,@getowner);  
   }

   CanPayCosts(who = $, lTargets = $, iSpellPower = 0)
   {
      local oWeapon;
      oWeapon = first(lTargets);
      if oWeapon = $ or not isClass(oWeapon,&weapon)
      {
	 Send(who, @MsgSendUser, #message_rsc=enchantweapon_no_can,
	    #parm1=send(oWeapon,@getdef),#parm2=send(oWeapon,@getname));
	 return False;
      }
      if Send(oWeapon,@CheckTypeFlag,#flag=ATCK_WEAP_MAGIC)
      {
	 Send(who, @MsgSendUser, #message_rsc=enchantweapon_already_done);
	 return False;
      }
      propagate;   % Check other things higher up
   }

   CastSpell(who = $, lTargets = $, iSpellPower = 0)
   {
      local oWeapon, oWeapAtt, iDuration;

      oWeapon = first(lTargets);
      if oWeapon = $ or not isClass(oWeapon,&weapon)
      {
	 Send(who, @MsgSendUser, #message_rsc=enchantweapon_no_can,
	    #parm1=send(oWeapon,@getdef),#parm2=send(oWeapon,@getname));
	 return False;
      }

      if not Send(oWeapon,@CanEnchant,#oSpell = self)
      {
         Send(who, @MsgSendUser, #message_rsc=spell_resists, 
             #parm1=send(oWeapon,@getCapdef),#parm2=send(oWeapon,@getname));
	 return;
      }
      
      if not Send(self,@IsTargetInRange,#who=who,#target=oWeapon)
      {  
	 Send(who, @MsgSendUser,#message_rsc=spell_out_of_range, 
	    #parm1=Send(oWeapon,@GetCapDef),#parm2=Send(oWeapon,@GetName));
	 return False;
      }     

      oWeapAtt = send(SYS,@finditemattbynum,#num=WA_ENCHANTED);	  
      send(oWeapAtt,@AddToItem,#oItem=oWeapon, #random_gen=FALSE,
	  #timer_duration =send(self,@GetDuration,#iSpellPower=iSpellPower),
     #state3=send(self,@GetMakerInfo,#who=who));

      Send(who, @MsgSendUser, #message_rsc=enchantedweapon_cast_rsc,
	 #parm1=Send(oWeapon,@GetName));

      propagate;
   }

   GetDuration(iSpellPower=0)
   {
      local iDuration;

      iDuration = random(1,4) + (iSpellPower/3);
      %% ~( 1 - 37 )
      %%Convert to hours:
      iDuration = iDuration * 1000 * 60 * 60;
      return iDuration;
   }
   
end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

