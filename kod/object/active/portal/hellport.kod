% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
HellPortal is Portal

constants:

   include blakston.khd
   SWITCH_DELAY_MIN = 5000
   SWITCH_DELAY_MAX = 10000

resources:

   hellportal_name_rsc = "Ein Dimensionstor"
   hellportal_icon_rsc = teleport.bgf
   hellportal_desc_rsc = "Als Du durch das Portal schaust, kannst Du auf der anderen Seite %s."

   hellportal_move_req_no = "Aus irgendeinem Grund kannst Du das Dimensionstor nicht betreten."

   hellportal_tos = "die belebte Brunnenkneipe ausmachen."
   hellportal_marion = "die abgelegene Kneipe zur lahmen Kröte ausmachen."
   hellportal_jasper = "die urige Kneipe zum Zwergenhammer ausmachen."
   hellportal_cornoth = "eine gemütliche Kneipe neben einem stillen Bach ausmachen."
   hellportal_barloque = "eine luxuriöse Kneipe in einer belebten Stadt ausmachen."

   hellportal_kocatan = "eine standhafte Inselfestung"

classvars:

   vrName = hellportal_name_rsc

   viObject_flags = MOVEON_TELEPORTER | DRAWFX_TRANSLUCENT_75

   viAnimationSpeed = 400

properties:
   
   vrIcon = hellportal_icon_rsc
   vrDesc = hellportal_desc_rsc

   piDest_room = RID_TOS_INN

   plPossibleLocations = $
   ptLocations = $

messages:

   Constructed()
   {
      plPossibleLocations = [RID_TOS_INN, RID_COR_INN, RID_BAR_INN, RID_JAS_INN, RID_MAR_INN ];

      % Set up the locations timer.
      Send(self,@LocationsTimer);

      propagate;
   }

   LocationsTimer()
   {
      ptLocations = $;

      send(self,@SwapLocations);
      ptLocations = Createtimer(self,@LocationsTimer,random(SWITCH_DELAY_MIN,SWITCH_DELAY_MAX));

      return;
   }

   SwapLocations()
   {
      local number,chosen, rand, oRoom;

      number = length(plPossibleLocations);

      chosen = piDest_Room;
      while chosen = piDest_room
      {
         rand = random(1,number);
         chosen = Nth(plPossibleLocations,rand);
      }

      oRoom = send(SYS,@FindRoomByNum,#num=chosen);

      % If room doesn't exist, don't change anything.
      if oRoom <> $
      {
         piDest_room = chosen;
         piDest_row = send(oRoom,@GetTeleportRow);
         piDest_col = send(oRoom,@GetTeleportCol);
      }

      return;
   }

   ShowDesc(who=$)
   "Okay, if you're from Ko'catan, you can only see the island."
   {
      local rLocation;

      if (poOwner <> $ AND IsClass(poOwner,&Underworld))
         AND send(who,@CheckPlayerFlag,#flag=PFLAG2_KOCATAN_DEATH,#flagset=2)
      {
         rLocation = hellportal_kocatan;
      }
      else
      {
         % Default
         rLocation = hellportal_tos; 

         if piDest_room = RID_MAR_INN
         {
            rLocation = hellportal_marion;
         }

         if piDest_room = RID_COR_INN
         {
            rLocation = hellportal_cornoth;
         }

         if piDest_room = RID_JAS_INN
         {
            rLocation = hellportal_jasper;
         }

         if piDest_room = RID_BAR_INN
         {
            rLocation = hellportal_barloque;
         }
      }

      AddPacket(4,vrDesc,4,rLocation);

      return;
   }

   TeleportSomething(what=$)
   "Called when something walks on top of us."
   {
      local oRoom;

      % Check for Ko'catanites.
      if poOwner <> $ and isClass(poOwner,&Underworld)
         AND IsClass(what,&Player)
         AND send(what,@CheckPlayerFlag,#flag=PFLAG2_KOCATAN_DEATH,#flagset=2)
      {
         % Allow them to head to the island.
         send(what,@SetPlayerFlag,#flag=PFLAG2_KOCATAN_DEATH,#value=FALSE,#flagset=2);
         oRoom = send(SYS,@FindRoomByNum,#num=RID_KOC_INN);
         send(oRoom,@Teleport,#what=what);

         return;
      }

      propagate;
   }

   Delete()
   {
      if ptLocations <> $
      {
         DeleteTimer(ptLocations);
         ptLocations = $;
      }

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

