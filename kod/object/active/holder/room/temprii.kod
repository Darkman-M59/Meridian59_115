% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
TempleRiija is Room

constants:

   include blakston.khd

   RESET_TIME = 30000   % five seconds

   SECTOR_DOOR = 4

resources:

   room_name_RiijaTemple = "Der Riija Tempel"
   room_RiijaTemple = ke1.roo
   riijatemple_music = bridge.mp3

   riijatemple_door1 = "~kDer Mönch von Riija sagt, \"~BKeiner ~rsoll ~B~bmein ~IAdyton ~kbetreten ~g~B!~n~k\""
   riijatemple_door2 = "~kDer Mönch von Riija sagt, \"~BVorsicht! ~I~bDu würdest ~rverrückt werden, ~gwenn Du es ~B~kwagtest, ~B~bdie Bibliothek des ~I~rschrecklichen ~BMalstromes zu betreten.\""
   riijatemple_door3 = "~kDer Mönch von Riija sagt, \"Der innere Klosterbereich ~I~rdarf nicht ~gvon Dir ~bbetreten ~Bwerden~n~k.\""
   riijatemple_door4 = "~kDer Mönch von Riija sagt, \"~b~BEs ist Dir verboten ~r~Bin die ~gbodenlosen ~I~kKatakomben ~n~gunter dem ~BTempel hinabzusteigen~n~k.\""
   riijatemple_door5 = "~kDer Mönch von Riija sagt, \"~rEs ist wohl besser, ~b~Idu meidest~I~r diese Tür...~n~k.\""
   riijatemple_door6 = "~kDer Mönch von Riija sagt, \"~B~rDiese Tür ~gführt ins ~bNirgendwo. Nicht, dass ~gich damit einen ~kleeren Raum oder eine ~rkahle Wand meine. ~bNirgendwo ~gist ein gewaltiges Fegefeuer, das jeden ~rverschlingt ~kder sich ~IZutritt ~nverschafft~k.~n~k\""

   riijatemple_sound = down2.wav

   riijaplayer_death_male_wav = malefall822m.wav
   riijaplayer_death_female_wav = femalefall822m.wav

   riijadeadmessage0a = "Autsch, die letzte Stufe war kaputt.\nDu erwachst, etwas benommen, an einem Ort, der eine Kneipe zu sein scheint."
   riijadeadmessage0b = "Autsch, die letzte Stufe war kaputt."

   riijadeadmessage1 = "Bist Du tot?"
   riijadeadmessage2 = "Nein, so fühlt sich der Tod nicht an..."
   riijadeadmessage3 = "er tut nicht gar so weh."
   riijadeadmessage4 = "Nein, nicht ganz. Du weißt zwar nicht, was geschehen ist, aber ..."
   riijadeadmessage5 = "Du hattest wahrscheinlich ganz schön Glück."

classvars:

   vrName = room_name_RiijaTemple

   viTeleport_row = 49
   viTeleport_col = 31

   viTerrain_type = TERRAIN_TEMPLE | TERRAIN_MYSTICAL | TERRAIN_JUNGLE | TERRAIN_BEACH | TERRAIN_MOUNTAIN

   viClientFlags = ROOM_OVERRIDE_DEPTH1
   viOverrideDepth1 = BRIDGE_OF_FAITH_LEVEL

properties:

   prRoom = room_RiijaTemple
   piRoom_num = RID_TEMPLE_RIIJA
   prMusic = riijatemple_music
   pbIllusioned = TRUE

   piBaseLight = LIGHT_NICE
   piOutside_factor = 3

   ptIllusionResetTimer = $
   ptDeathMessageTimer = $
   poDead = $

messages:

   Constructor()
   {
      propagate;
   }

   Teleport(what=$)
   {
      local iRow, iCol, oRoom;
      
      if send(what,@GetOwner) = self
      {
         % Someone is casting blink.  If on the island, blink them back to the city.
         iRow = send(what,@GetRow);
         iCol = send(what,@GetCol);

         if ((iRow >= 4 and iCol >= 13) and (iRow <= 30 and iCol <= 44))
         {
            oRoom = send(SYS,@FindRoombyNum,#num=RID_KOC_NORTH);            
            post(oRoom,@Teleport,#what=what);
            
            return TRUE;
         }
      }
      
      propagate;
   }

   NewHold(what=$,new_row=0,new_col=0)
   {
      local iRow, iCol;
      
      iRow = send(what,@GetRow);
      iCol = send(what,@GetCol);
      
      if IsClass(what,&Player)
         AND ((New_Row >= 4 AND New_Col >= 13)
              AND (New_Row <= 30 AND New_Col <= 44))
         AND NOT (IsClass(what,&DM) AND send(what,@PlayerIsImmortal))
      {
         post(self,@Teleport,#what=what);
      }
      
      propagate;
   } 

   SomethingTryGo(what = $,row = $,col = $)
   {
      if pbIllusioned
      { 
         if row = 16 AND col = 31
         {
            send(what,@MsgSendUser,#message_rsc=riijatemple_door1);

            return TRUE;
         }

         if row = 19 AND col = 31
         {
            send(what,@MsgSendUser,#message_rsc=riijatemple_door2);

            return TRUE;
         }

         if row = 19 AND col = 30
         {
            send(what,@MsgSendUser,#message_rsc = riijatemple_door3);

            return TRUE;
         }

         if row = 11 AND col = 33
         {
            send(what,@MsgSendUser,#message_rsc = riijatemple_door4);

            return TRUE;
         }

         if row = 16 AND col = 30 
         {
            send(what,@MsgSendUser,#message_rsc = riijatemple_door5);

            return TRUE;
         }

         if row = 11 AND col = 28
         {
            send(what,@MsgSendUser,#message_rsc = riijatemple_door6);

            return TRUE;
         }
      }
      else
      {
         % Don't let people enter the front door while not illusioned
         if (row = 23 OR row = 24)
            AND (col = 30 OR col = 31)
         {
            % Return without message.  That'll drive the conspiracy theorists wild!
            return TRUE;
         }
      }

      propagate;
   }

   DispelIllusions(what=$)
   {
      local oMonk;

      oMonk = send(self,@FindHoldingActive,#class=&RiijaMonk);

      if oMonk <> $
      {
         send(oMonk,@DispelIllusions);
      }

      send(self,@DoDispelIllusionEffect);

      pbIllusioned = FALSE;

      % Reappear in 30 seconds.
      ptIllusionResetTimer = createtimer(self,@ReplaceIllusions,30000);
      
      propagate;
   }

   DoDispelIllusionEffect()
   {
      % Must be able to be called redundantly.
      % May be sent even if effect is already in that position.

      send(self,@ChangeTexture,#id=2,#new_texture=02301,#flags=CTF_FLOOR);
      send(self,@ChangeTexture,#id=4,#new_texture=02301,#flags=CTF_FLOOR);
      send(self,@ChangeTexture,#id=6,#new_texture=02301,#flags=CTF_FLOOR); 
      send(self,@ChangeTexture,#id=7,#new_texture=02305,#flags=CTF_FLOOR); 
      send(self,@ChangeTexture,#id=8,#new_texture=02305,#flags=CTF_FLOOR);

      send(self,@ChangeTexture,#id=2,#new_texture=0,#flags=CTF_CEILING); 
      send(self,@ChangeTexture,#id=4,#new_texture=0,#flags=CTF_CEILING);
      send(self,@ChangeTexture,#id=5,#new_texture=0,#flags=CTF_CEILING); 
      send(self,@ChangeTexture,#id=6,#new_texture=09418,#flags=CTF_CEILING);
      send(self,@ChangeTexture,#id=7,#new_texture=08823,#flags=CTF_CEILING); 
      send(self,@ChangeTexture,#id=8,#new_texture=08823,#flags=CTF_CEILING);
      send(self,@ChangeTexture,#id=9,#new_texture=08823,#flags=CTF_CEILING);

      send(self,@ChangeTexture,#id=10,#new_texture=0,#flags=CTF_NORMALWALL);
      send(self,@ChangeTexture,#id=10,#new_texture=09418,#flags=CTF_BELOWWALL);
      send(self,@ChangeTexture,#id=11,#new_texture=09418,#flags=CTF_ABOVEWALL);

      send(self,@SetSector,#sector=2,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      send(self,@SetSector,#sector=3,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      send(self,@SetSector,#sector=4,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      send(self,@SetSector,#sector=5,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      send(self,@SetSector,#sector=6,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      send(self,@SetSector,#sector=7,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      send(self,@SetSector,#sector=8,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);
      send(self,@SetSector,#sector=9,#animation=ANIMATE_FLOOR_LIFT,#height=392,#speed=0);

      send(self,@SetSector,#sector=2,#animation=ANIMATE_CEILING_LIFT,#height=544,#speed=0);
      send(self,@SetSector,#sector=3,#animation=ANIMATE_CEILING_LIFT,#height=544,#speed=0);
      send(self,@SetSector,#sector=4,#animation=ANIMATE_CEILING_LIFT,#height=544,#speed=0);
      send(self,@SetSector,#sector=5,#animation=ANIMATE_CEILING_LIFT,#height=544,#speed=0);
      send(self,@SetSector,#sector=6,#animation=ANIMATE_CEILING_LIFT,#height=472,#speed=0);
      send(self,@SetSector,#sector=7,#animation=ANIMATE_CEILING_LIFT,#height=488,#speed=0);
      send(self,@SetSector,#sector=8,#animation=ANIMATE_CEILING_LIFT,#height=488,#speed=0);
      send(self,@SetSector,#sector=9,#animation=ANIMATE_CEILING_LIFT,#height=488,#speed=0);

      return;
   }

   ReplaceIllusions()
   {
      local i, each_obj;
   
      ptIllusionResetTimer = $;

      for i in plActive
      {
         each_obj = send(self,@HolderExtractObject,#data=i);
         
         % Don't let illusion reappear when people are in the temple!
         if send(each_obj,@GetRow) > 8
            AND send(each_obj,@GetRow) < 24
            AND send(each_obj,@GetCol) > 23
            AND send(each_obj,@GetCol) < 38
            AND isclass(each_obj,&player)
         {
            % Try again in 30 seconds
            ptIllusionResetTimer = createtimer(self,@ReplaceIllusions,30000);  
            
            return;     
         }
      }
   
      pbIllusioned = TRUE;   

      send(self,@DoReplaceIllusionEffect);

      return;   
   }

   DoReplaceIllusionEffect()
   {
      send(self,@ChangeTexture,#id=2,#new_texture=09415,#flags=CTF_FLOOR); 
      send(self,@ChangeTexture,#id=4,#new_texture=09414,#flags=CTF_FLOOR);
      send(self,@ChangeTexture,#id=6,#new_texture=09414,#flags=CTF_FLOOR); 
      send(self,@ChangeTexture,#id=7,#new_texture=09414,#flags=CTF_FLOOR); 
      send(self,@ChangeTexture,#id=8,#new_texture=09414,#flags=CTF_FLOOR);

      send(self,@ChangeTexture,#id=2,#new_texture=09415,#flags=CTF_CEILING); 
      send(self,@ChangeTexture,#id=4,#new_texture=09416,#flags=CTF_CEILING);
      send(self,@ChangeTexture,#id=5,#new_texture=09416,#flags=CTF_CEILING); 
      send(self,@ChangeTexture,#id=6,#new_texture=09416,#flags=CTF_CEILING);
      send(self,@ChangeTexture,#id=7,#new_texture=09416,#flags=CTF_CEILING); 
      send(self,@ChangeTexture,#id=8,#new_texture=09416,#flags=CTF_CEILING);
      send(self,@ChangeTexture,#id=9,#new_texture=09416,#flags=CTF_CEILING);

      send(self,@ChangeTexture,#id=10,#new_texture=09417,#flags=CTF_NORMALWALL); 
      send(self,@ChangeTexture,#id=10,#new_texture=09417,#flags=CTF_BELOWWALL);
      send(self,@ChangeTexture,#id=11,#new_texture=09417,#flags=CTF_ABOVEWALL);

      send(self,@SetSector,#sector=2,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      send(self,@SetSector,#sector=3,#animation=ANIMATE_FLOOR_LIFT,#height=536,#speed=0);
      send(self,@SetSector,#sector=4,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      send(self,@SetSector,#sector=5,#animation=ANIMATE_FLOOR_LIFT,#height=480,#speed=0);
      send(self,@SetSector,#sector=6,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      send(self,@SetSector,#sector=7,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      send(self,@SetSector,#sector=8,#animation=ANIMATE_FLOOR_LIFT,#height=408,#speed=0);
      send(self,@SetSector,#sector=9,#animation=ANIMATE_FLOOR_LIFT,#height=480,#speed=0);

      send(self,@SetSector,#sector=2,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      send(self,@SetSector,#sector=3,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      send(self,@SetSector,#sector=4,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      send(self,@SetSector,#sector=5,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      send(self,@SetSector,#sector=6,#animation=ANIMATE_CEILING_LIFT,#height=504,#speed=0);
      send(self,@SetSector,#sector=7,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      send(self,@SetSector,#sector=8,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);
      send(self,@SetSector,#sector=9,#animation=ANIMATE_CEILING_LIFT,#height=536,#speed=0);

      return;
   }

   SomethingMoved(what = $,new_row = $, new_col = $)
   {
      local i;

      if IsClass(what,&Player)
         AND ((new_row >= 31 AND new_col >= 26) AND (new_row <= 44 AND new_col <= 29))
         OR ((new_row >= 31 AND new_col >= 32) AND (new_row <= 45 AND new_col <= 35))
         OR (new_row = 45 AND new_col = 29)
      {
         send(self,@PlayerHurledSelfOffCliffs,#what=what);
      }
      
      if (new_row > 32) AND (new_row < 45) 
         AND ((new_col = 30) OR (new_col = 31))
         AND NOT (IsClass(what,&DM) AND send(what,@PlayerIsImmortal))
      {
         %% on the bridge.  Do they have a sword of riija?
         if isClass(what,&Monster)  
         {
            send(what,@Killed);
            
            return;
         }
                  
         for i in send(what,@GetPlayerUsing)
         {
            %% checking item to see if it's a riija sword.
            if isClass(i,&RiijaSword) AND (send(i,@GetQuester) = what)
            {
               propagate;
            }
         } 

         % No sword.  Kill 'em
         send(self,@PlayerHurledSelfOffCliffs,#what=what);   
      }
      
      propagate;
   }

   PlayerHurledSelfOffCliffs(what=$)
   {
      local i, dropped, row, col;

      row = send(what,@GetRow);
      col = send(what,@GetCol);

      debug(send(what,@GetName),"Hurled self off cliffs");
      
      dropped = FALSE;
      for i in send(what,@GetPlayerUsing)
      {
         if send(i,@ReqLeaveOwner)
            AND random(1,3) = 1
         {
            send(self,@NewHold,#what=i,#new_row=send(what,@GetRow),#new_col=send(what,@GetCol));
            dropped = TRUE;
            
            break;
         } 
      }
      
      if NOT dropped
      {
         for i in send(what,@GetHolderPassive)
         {
            if send(i,@ReqLeaveOwner)
            AND random(1,3) = 1
            {
               send(self,@NewHold,#what=i,#new_row=send(what,@GetRow),#new_col=send(what,@GetCol));
               
               break;
            } 
         }
      }

      send(what,@EffectSendUserXLat,#xlat=PT_ALLRED);
      send(what,@EffectSendUser,#what=what,#effect=EFFECT_PARALYZE_ON);

      if poDead <> $
      {
         send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage0a);
         send(SYS,@UtilGotoSquare,#what=what,#where=send(SYS,@FindRoomByNum,#num=RID_KOC_INN),
              #new_row=8,#new_col=6,#new_angle=ANGLE_NORTH);
         send(what,@LoseHealth,#amount=(send(what,@GetHealth)-1));
         send(send(sys,@FindSpellByNum,#num=SID_HOLD),@DoHold,
              #what=self,#oTarget=what,#iDurationSecs=5,#report=FALSE,#bAllowFreeAction=FALSE);
         send(what,@EffectSendUserDuration,#effect=EFFECT_PAIN,#duration=2000);
         send(what,@EffectSendUserXLat,#xlat=0);
         send(what,@EffectSendUser,#what=what,#effect=EFFECT_PARALYZE_OFF);
      }
      else
      {
         poDead = what;   
         send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage0b);
   
         send(SYS,@UtilGotoSquare,#what=what,
              #where=send(SYS,@FindRoomByNum,#num=RID_KOC_INN),
              #new_row=8,#new_col=6,#new_angle=ANGLE_NORTH);
         send(what,@Losehealth,#amount=(send(what,@GetHealth)-1));
         send(send(sys,@FindSpellByNum,#num=SID_HOLD),@DoHold,#oTarget=what,#iDurationSecs=30,#report=FALSE);
         
         ptDeathMessageTimer = createtimer(self,@TimerMessage1,5000); 
      }
      
      if send(what,@GetGender) = GENDER_MALE
      {
         send(self,@SomethingWaveRoom,#row=row,#col=col,#wave_rsc=riijaplayer_death_male_wav);
         send(what,@WaveSendUser,#what=what,#wave_rsc=riijaplayer_death_male_wav);
      }
      else
      {
         send(self,@SomethingWaveRoom,#row=row,#col=col,#wave_rsc=riijaplayer_death_female_wav);
         send(what,@WaveSendUser,#what=what,#wave_rsc=riijaplayer_death_female_wav);
      }
      
      return;
   }

   TimerMessage1()
   {
      ptDeathMessageTimer = $;
      send(poDead,@MsgSendUser, #message_rsc=riijadeadmessage1);
      ptDeathMessageTimer=createtimer(self,@TimerMessage2,5000);
      
      return;
   }

   TimerMessage2()
   {
      ptDeathMessageTimer = $;
      send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage2);
      ptDeathMessageTimer = createtimer(self,@TimerMessage3,5000);
      
      return;
   }

   TimerMessage3()
   {
      ptDeathMessageTimer = $;
      send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage3);
      ptDeathMessageTimer = createtimer(self,@TimerMessage4,5000);
      
      return;
   }

   TimerMessage4()
   {
      ptDeathMessageTimer = $;
      send(poDead,@MsgSendUser, #message_rsc=riijadeadmessage4);
      ptDeathMessageTimer = createtimer(self,@TimerMessage5,5000);
      
      return;
   }

   TimerMessage5()
   {
      ptDeathMessageTimer = $;
      send(poDead,@MsgSendUser,#message_rsc=riijadeadmessage5);
      send(poDead,@EffectSendUserDuration,#effect=EFFECT_PAIN,#duration=2000);
      send(poDead,@EffectSendUserXLat,#xlat=0);
      send(poDead,@EffectSendUser,#what=poDead,#effect=EFFECT_PARALYZE_OFF);
      poDead = $;
      
      return;  
   }

   CreateStandardObjects()
   {
      % teacher
      send(self,@NewHold,#what=Create(&RiijaMonk),
           #new_row=10,#new_col=31,#fine_row=32,#fine_col=0,#new_angle=ANGLE_SOUTH);

      propagate;
   }

   CreateStandardExits()
   {
      plExits = $;

      plExits = Cons([ 24, 30, RID_TEMPLE_RIIJA, 22, 30, ROTATE_NONE ],plExits);
      plExits = Cons([ 24, 31, RID_TEMPLE_RIIJA, 22, 31, ROTATE_NONE ],plExits);
      plExits = Cons([ 23, 30, RID_TEMPLE_RIIJA, 25, 30, ROTATE_NONE ],plExits);
      plExits = Cons([ 23, 31, RID_TEMPLE_RIIJA, 25, 31, ROTATE_NONE ],plExits);

      plEdge_exits = $;
      plEdge_Exits = Cons([LEAVE_WEST, RID_KD2, 12, 69, ROTATE_NONE], plEdge_exits);
      plEdge_Exits = Cons([LEAVE_SOUTH, RID_KE2, 1, 37, ROTATE_NONE], plEdge_exits);

      propagate;
   }

   Delete()
   {
      if ptIllusionResetTimer <> $
      {
         deletetimer(ptIllusionResetTimer);
         ptIllusionResetTimer = $;
      }
      
      if ptDeathMessageTimer <> $
      {
         deletetimer(ptDeathMessageTimer);
         ptDeathMessageTimer = $;
      }

      poDead = $;
      
      propagate;
   }

   ModifyChanceToImprove(who=$,oSpellSkill=$,chance=0)
   "This takes a spell/skill that who is trying to advance and modifies the improve_chance."
   {
      if IsClass(oSpellSkill,&Spell)
         AND send(oSpellSkill,@GetSchool) = SS_RIIJA
      {
         % Give a 10% bonus.
         return ((chance * 11)/10);
      }

      return chance;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

