% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueCourt is BarloqueRoom

constants:

   include blakston.khd

resources:

   room_name_barloque_court = "Büro des Justiziars"
   room_barloque_court = barcourt.roo
   
   news_justicar = "Das Buch von Jala"
   news_justicar_desc = "Aufzeichnungen der Geschehnisse im Büros des Justiziars."


   BarloqueCourt_sign_name = "Mitteilungen des Justiziars"
   BarloqueCourt_sign_icon = newbsign.bgf

   BarloqueCourt_sign_desc = "In das Schild ist eingraviert:\n\nDas Amt des Justiziars ist sehr wichtig.  Die Aufgabe des Justiziars ist es diejenigen zu begnadigen, welche nicht länger die Schande eines Kriminellen oder Mörders zu tragen haben.  Der Justiziar wird von den Bürgern des Landes gewählt.\n\nUm Abzustimmen, kaufe einem Stimmzettel von Camaro.  Schreiben darauf den Namen der Person, für welche du abstimmen möchtest und gib den Stimmzettel zurück.  Bitte beachtet das nur unbescholtene Bürger wählen dürfen.\n\nAls Justiziar bekommst du von Camaro einen Brief mit allen Rechten und Pflichten welche dieses Amt mit sich bringt.\n\nCaramo wird ankündigen wann eine Wahl stattfindet.  Falls du informiert werden möchtest wann eine neue Wahl beginnt, sage 'informieren' zu Caramo. Falls du nicht mehr informiert werden möchtest, sage zu ihr 'nicht informieren'. Gesetzlose und Mörder werden nicht informiert, da sind ohnehin nicht daran teilnehmen dürfen."



















classvars:

   vrName = room_name_barloque_court

   viTeleport_row = 6
   viTeleport_col = 12

   viPermanent_flags = ROOM_NO_COMBAT
   viTerrain_Type = TERRAIN_CITY | TERRAIN_SHOP

properties:

   prRoom = room_barloque_court
   piRoom_num = RID_BAR_COURT

   piBaseLight = LIGHT_NICE
   piOutside_factor = 0

   poCaramo = $
   poBookOfJala = $

messages:

   % The gallery is separated from the official courtroom by a wall.
   % Only those who are in the courtroom area are subject to Justicar
   %  commands.  This allows others to watch a trial and not be abused.

   ReqInSpecialArea(row = $, col = $, obj = $)
   {
      local iRow, iCol;

      if obj <> $
      {
         iRow = Send(obj,@GetRow);
         iCol = Send(obj,@GetCol);
      }
      else
      {
         iRow = row;
         iCol = col;
      }

      if iRow >= 7
      {
         return TRUE;
      }

      return FALSE;
   }

   CreateStandardObjects()
   {
      local oSign;

      % The clerk     
      poCaramo = Send(SYS,@GetCaramo);
      if poCaramo = $
      {
         poCaramo = Create(&BarloqueClerk);
      }
      
      Send(self,@NewHold,#what=poCaramo,
           #new_row=12,#new_col=12,#fine_row=28,#fine_col=28,
           #new_angle=ANGLE_WEST);

      % A guard
      Send(self,@NewHold,#what=Create(&DukeGuard),
           #new_row=10,#new_col=1,#fine_row=60,#fine_col=28,
           #new_angle=ANGLE_EAST);

      % A book of jala
      poBookOfJala = Send(SYS,@FindNewsByNum,#num=NID_JUSTICAR);
      if poBookOfJala = $
      {
         poBookOfJala = Create(&NewsJusticar,#nid=NID_JUSTICAR,
                               #name=news_justicar,#desc=news_justicar_desc);
      }
      
      Send(self,@NewHold,#what=poBookOfJala,
           #new_row=2,#new_col=11,#fine_row=44,#fine_col=44,
           #new_angle=ANGLE_SOUTH_WEST);

      % A information sign
      oSign = Create(&Sign,#name=BarloqueCourt_sign_name,
                     #icon=BarloqueCourt_sign_icon,
                     #desc=BarloqueCourt_sign_desc);
      Send(self,@NewHold,#what=oSign,#new_row=11,#new_col=12,
           #fine_row=0,#fine_col=32);

      % Braziers
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=1,#new_col=6,
           #fine_row=28,#fine_col=60);
      Send(self,@NewHold,#what=Create(&Brazier),#new_row=13,#new_col=1,
           #fine_row=39,#fine_col=44);

      % Table
      Send(self,@NewHold,#what=Create(&Table),#new_row=2,#new_col=9,
           #fine_row=28,#fine_col=28);

      % Candelabra
      Send(self,@NewHold,#what=Create(&Candelabra),#new_row=12,#new_col=11,
           #fine_row=60,#fine_col=52);

      propagate;
   }

   CreateStandardExits()
   {
      plExits = $;

      % TO TOWN
      plExits = Cons([ 2, 4, RID_BAR_NORTH, 33, 54, ROTATE_NONE ],plExits); %% north
      plExits = Cons([ 4, 2, RID_BAR_NORTH, 38, 51, ROTATE_NONE ],plExits); %% west

      % GALLERY GATE
      plExits = Cons([ 6, 11, RID_BAR_COURT, 8, 11, ROTATE_NONE ],plExits); %% to court
      plExits = Cons([ 7, 11, RID_BAR_COURT,  5, 11, ROTATE_NONE ],plExits); %% to gallery

      propagate;
   }

   RecalcLightAndWeather()
   {
      Send(poCaramo,@NewHour);
      
      propagate;
   }

   GetClerk()
   {
      return poCaramo;
   }

   GetBook()
   {
      return poBookOfJala;
   }
   
   Delete()
   {
      if poCaramo <> $
      {
         Send(poCaramo,@NewOwner,#what=$);
         poCaramo = $;
      }
      
      poBookofJala = $;

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

