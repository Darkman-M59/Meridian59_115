% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanInn is KocatanRoom

constants:

   include blakston.khd

resources:
  
   room_kocinn = kocinn.roo
   room_name_kocinn = "Das Gästehaus"

   kocinn_waz_locked = "Raum waz ist verschlossen."
   kocinn_wazwaz_locked = "Raum wazwaz ist verschlossen."
   kocinn_zik_locked = "Raum zik ist verschlossen."
   kocinn_zikzik_locked = "Raum zikzik ist verschlossen."
   kocinn_katalin_locked = "Raum katalin ist verschlossen."
   kocinn_katalinwaz_locked = "Raum katalinwaz ist verschlossen."

   kocnews_god_name = "Neues von den Designern"
   kocnews_god_desc = "Neueste Informationen von den Administratoren."

   kocinn_music = song12.mp3

classvars:

   vrName = room_name_kocinn

   viTeleport_row = 4
   viTeleport_col = 5

   viTerrain_Type = TERRAIN_CITY | TERRAIN_SHOP
   viPermanent_flags = ROOM_NO_COMBAT | ROOM_SANCTUARY | ROOM_SAFELOGOFF

properties:

   prRoom = room_kocinn
   piRoom_num = RID_KOC_INN

   piBaseLight = LIGHT_NICE
   piOutside_factor = 3

   prMusic = kocinn_music

messages:

   CreateStandardExits()
   {
      plExits = $;

      %% to kocatan
      plExits = Cons([ 2, 5, RID_KOCATAN, 28, 7, ROTATE_NONE ],plExits);
      
      %% to kocatan tavern
      plExits = Cons([ 12, 8, RID_KOC_TAVERN, 16, 17, ROTATE_NONE ],plExits);

      plExits = Cons([ 5, 2, ROOM_LOCKED_DOOR, kocinn_waz_locked],plExits);
      plExits = Cons([ 6, 2, ROOM_LOCKED_DOOR, kocinn_waz_locked],plExits);
      plExits = Cons([ 5, 3, ROOM_LOCKED_DOOR, kocinn_waz_locked],plExits);
      plExits = Cons([ 6, 3, ROOM_LOCKED_DOOR, kocinn_waz_locked],plExits);

      plExits = Cons([ 10, 3, ROOM_LOCKED_DOOR, kocinn_wazwaz_locked],plExits);
      plExits = Cons([ 10, 4, ROOM_LOCKED_DOOR, kocinn_wazwaz_locked],plExits);
      
      plExits = Cons([ 10, 6, ROOM_LOCKED_DOOR, kocinn_zik_locked],plExits);
      %% 10,7 finecol is less than 32 has to be hardcoded

      plExits = Cons([ 10, 8, ROOM_LOCKED_DOOR, kocinn_katalinwaz_locked],plExits);
      %% 11,8 finerow is less than 32 has to be hardcoded

      plExits = Cons([ 7, 6, ROOM_LOCKED_DOOR, kocinn_zikzik_locked],plExits);
      %% 7, 5 finecol is greater than 32 has to be hardcoded

      plExits = Cons([ 8, 6, ROOM_LOCKED_DOOR, kocinn_katalin_locked],plExits);
      %% 9, 6 finerow less than 32 has to be hardcoded

      propagate;
   }

   SomethingTryGo(what=$, row=0, col=0, fine_row=0, fine_col=0)
   {
      if (row=10) and (col=7) and (fine_col < 32)
      {
         send(what,@msgsenduser,#message_rsc=kocinn_zik_locked);	 
         return TRUE;
      }
      
      if (row=11) and (col=8) and (fine_row < 32)
      {
         send(what,@msgsenduser,#message_rsc=kocinn_katalinwaz_locked);
         return TRUE;
      }
      
      if (row=7) and (col=5) and (fine_col > 32)
      {
         send(what,@msgsenduser,#message_rsc=kocinn_zikzik_locked);          
         return TRUE;
      }
      
      if (row=9) and (col=6) and (fine_row < 32)
      {
         send(what,@msgsenduser,#message_rsc=kocinn_katalin_locked);
         return TRUE;
      }
      
      propagate;
   }

   CreateStandardObjects()
   {
      local lObjCoords1, oNews;

      oNews = Create(&NewsLink,#nid=NID_ANNOUNCEMENTS,
	                  #name=kocnews_god_name,#desc=kocnews_god_desc);
      Send(self,@NewHold,#what=oNews,#new_row=4,#new_col=6,#fine_row=8,#fine_col=56);

      lObjCoords1 = [ [02,09,08,16,OO_JUG],[12,04,48,08,OO_JUG],[11,09,40,16,OO_GREY_JUG] ];

      Send(self,@CreateOrnObjFromList,#lObjlist=lObjCoords1);
      
      send(self,@NewHold,#what=Create(&KocatanInnkeeper),
           #new_row=5,#new_col=6,#fine_row=48,#fine_col=56,
           #new_angle=ANGLE_EAST);

      propagate;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

