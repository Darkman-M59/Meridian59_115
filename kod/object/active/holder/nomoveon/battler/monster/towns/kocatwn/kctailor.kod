% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanTailor is KocatanTown
constants:

   include blakston.khd

resources:

   KocatanTailor_name_rsc = "Tepal ix'Zonak"
   kocatanTailor_icon_rsc = kctailor.bgf
   kocatanTailor_desc_rsc = "Tepals schnelle, verstohlene Blicke zeigen die hohe, nervöse Energie, die ihm innewohnt. Irgendetwas an ihm sagt Dir, dass er Gründe hat, nervös zu sein. "



   kocatanTailor_entry_welcome = "Oh! -- Entschuldigung, Du hast mich überrascht. Kann ich Dir irgendwie helfen?"
   kocatantailor_sanctuary = "Zuflucht"
   kocatantailor_sanctuary_response = "Ahhh... Ich könnte Dir eine Zuflucht bieten ... aber das hat natürlich seinen Preis."
   
   kocatantailor_Stones = "Wertvolle Steine"
   kocatantailor_stones_response = "Es gibt einen echten Markt für wertvolle Steine im Moment. Jemand, der etwas Risiko auf sich nimmt, könnte eine Menge Geld machen. Zufällig weiß ich ein bißchen etwas darüber."

   
   KocatanTailor_new_look = "neue Kleider"
   KocatanTailor_new_look_r = "Ich denke, Du wirst mit meiner Wahl zufrieden sein."
   KocatanTailor_kriipa = "Die Kriipa"
   KocatanTailor_kriipa_r = "Die Schuppen der Kriipa sind rar. Trotzdem habe ich hier ein paar für Dich."
   KocatanTailor_mutant_ant = "Die rote Ameise"
   KocatanTailor_mutant_ant_r = "Ich mag diese tiefe rote Farbe wirklich, auch wenn sie von einem schrecklichen Insekt kommt."
   KocatanTailor_dragonfly = "Die Drachenfliegenkönigin"
   KocatanTailor_dragonfly_r = "Sie haben eine wunderschöne golden glänzende Haut, stimmt's?"
   KocatanTailor_yeti = "Yeti"
   KocatanTailor_yeti_r = "Du musst Moxal danken, er gab mir einst das Fell eines toten Yetis."
   KocatanTailor_fey_elhai = "Der Fey Elhai"
   KocatanTailor_fey_elhai_r = "Sie glitzern weiß und grau, dies ist das ähnlichste Pigment, das ich finden kann."
   KocatanTailor_fey_dirhai = "Der Fey Dirhai"
   KocatanTailor_fey_dirhai_r = "Es ist sehr schwer, dem glänzenden Blau nahezukommen, und dies ist mein gelungenster Versuch."
   KocatanTailor_avar = "Avar-Kämpfer"
   KocatanTailor_avar_r = "Die Federn haben einen wundervollen goldenen Glanz, wenn man sie im Sonnenlicht betrachtet."
   KocatanTailor_shadow = "Schattenwesen"
   KocatanTailor_shadow_r = "Und woher weißt Du, welche Farbe das Schattenwesen hat?"

   KocatanTailor_sapphire_sale = "Ich habe %s%s vom Festland importiert. Ich liebe diese klare blaue Farbe."
   KocatanTailor_emerald_sale = "Ich kann Dir %s%s verkaufen. Sie stammen vom Festland, was sie nicht gerade billig macht."

classvars:

   vrName = KocatanTailor_name_rsc
   vrIcon = KocatanTailor_icon_rsc
   vrDesc = KocatanTailor_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE | MOB_COND_SELLER | MOB_BUYER
   viOccupation = MOB_ROLE_TAILOR

properties:
   
messages:

   AffectMood(what=$,why=$)
   {
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood - 3);
         return;
      }
      
      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood + 5);
         return;
      }
      
      if why = MOODMOD_SELL_ITEM
      {
         if IsClass(what, &SanctuaryKey)
         {
            send(self,@SetMood,#new_mood=piMood + 2);
            return;
         }
         
         if IsClass(what, &NeruditeOreChunk) 
         {
            send(self,@SetMood,#new_mood=piMood + 3);
            return;
         }
         else
         {
            send(self,@SetMood,#new_mood=piMood + 1);
         }
         
         return;
      }
      
      return;
   }

   InitCondSale()
   {
      local oObj, oObj2, oObj3, oObj4;
      
      oObj=Create(&SanctuaryKey);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[kocatantailor_sanctuary],
           #action=[LIBACT_CONDITIONAL,oObj,1000,kocatantailor_sanctuary_response]);
           
      oObj2=Create(&NeruditeOreChunk);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[kocatantailor_stones],
           #action=[LIBACT_CONDITIONAL,oObj2,1200,kocatantailor_stones_response]);

      oObj3=Create(&Sapphire, #number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj3,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj3,1000,KocatanTailor_sapphire_sale]);

      oObj4=Create(&Emerald, #number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj4,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj4,1000,KocatanTailor_emerald_sale]);

      return;
   }

   UserEntered(who = $)
   {
      local iFlags;
      
      iFlags = send(who, @GetObjectFlags);
      if iFlags & PLAYER_PK
      {
         send(self,@SetMood,#new_mood=piMood + 3 );
      }

      if iFlags & PLAYER_OUTLAW
      {
         send(self,@SetMood,#new_mood=piMood + 2 );
      }
      
      Post(poOwner,@SomeoneSaid,#what = self, #string=KocatanTailor_entry_welcome,
           #type = SAY_RESOURCE);
         
      return;
   }

   SomeoneSaid(what = $,type = $,string = $)
   {
      if type <> SAY_NORMAL
      {
         propagate;
      }
      
      if NOT isClass(what,&Player)
      {
         propagate;
      }


      if NOT send(send(SYS,@GetWarEvent),@IsActive)
      {
         if NOT stringContain(string,"Tepal")
         {
            propagate;
         }

         if stringContain(string,KocatanTailor_new_look)
         {
            send(what,@SetDefaultClothes,#rand=TRUE);
            send(self,@Say,#message_rsc=KocatanTailor_new_look_r);

            propagate;
         }

         if stringContain(string,KocatanTailor_kriipa)
         {
            send(what,@SetDefaultClothes,#shirt_color=XLAT_TO_GREEN);
            send(self,@Say,#message_rsc=KocatanTailor_kriipa_r);

            propagate;
         }

         if stringContain(string,KocatanTailor_mutant_ant)
         {
            send(what,@SetDefaultClothes,#shirt_color=XLAT_TO_RED);
            send(self,@Say,#message_rsc=KocatanTailor_mutant_ant_r);

            propagate;
         }

         if stringContain(string,KocatanTailor_dragonfly)
         {
            send(what,@SetDefaultClothes,#shirt_color=XLAT_TO_ORANGE);
            send(self,@Say,#message_rsc=KocatanTailor_dragonfly_r);

            propagate;
         }

         if stringContain(string,KocatanTailor_yeti)
         {
            send(what,@SetDefaultClothes,#shirt_color=XLAT_TO_SKY);
            send(self,@Say,#message_rsc=KocatanTailor_yeti_r);

            propagate;
         }

         if stringContain(string,KocatanTailor_fey_elhai)
         {
            send(what,@SetDefaultClothes,#shirt_color=XLAT_TO_GRAY);
            send(self,@Say,#message_rsc=KocatanTailor_fey_elhai_r);

            propagate;
         }

         if stringContain(string,KocatanTailor_fey_dirhai)
         {
            send(what,@SetDefaultClothes,#shirt_color=XLAT_TO_BLUE);
            send(self,@Say,#message_rsc=KocatanTailor_fey_dirhai_r);

            propagate;
         }

         if stringContain(string,KocatanTailor_avar)
         {
            send(what,@SetDefaultClothes,#shirt_color=XLAT_TO_YELLOW);
            send(self,@Say,#message_rsc=KocatanTailor_avar_r);

            propagate;
         }

         if stringContain(string,KocatanTailor_shadow)
         {
            send(what,@SetDefaultClothes,#shirt_color=XLAT_TO_PURPLE);
            send(self,@Say,#message_rsc=KocatanTailor_shadow_r);

            propagate;
         }
      }

      propagate;
   }

   ObjectDesired(what=$)
   {
      if IsClass(what,&NumberItem) and
         Send(what,@GetNumber) > 100
      {
         return FALSE;
      }

      if Send(self,@IsObjectGem,#what=what) or
         Send(self,@IsObjectSundry,#what=what)
      {
	      return TRUE;
      }

      return FALSE;
   }

   SetForSale()
   {
      plFor_sale=[[
                   Create(&PantsC,#translation=PT_GRAY_TO_LGREEN),
                   Create(&PantsC,#translation=PT_GRAY_TO_LBGREEN),
                   Create(&PantsC,#translation=PT_GRAY_TO_LSKY),
                   Create(&PantsC,#translation=PT_GRAY_TO_LBLUE),
                   Create(&PantsC,#translation=PT_GRAY_TO_LPURPLE),
                   Create(&PantsC,#translation=PT_GRAY_TO_LGOLD),
                   Create(&PantsC,#translation=PT_GRAY_TO_LBBLUE),
                   Create(&PantsC,#translation=PT_GRAY_TO_LRED),
                   Create(&PantsC,#translation=PT_GRAY_TO_LORANGE),
                      
                   Create(&ShortSkirt,#color=XLAT_TO_ORANGE),
                   Create(&ShortSkirt,#color=XLAT_TO_RED),
                   Create(&ShortSkirt,#color=XLAT_TO_SKY),                   
                   Create(&ShortSkirt,#color=XLAT_TO_GREEN),
                   Create(&ShortSkirt,#color=XLAT_TO_BLACK),
                   Create(&ShortSkirt,#color=XLAT_TO_YELLOW),
                   Create(&ShortSkirt,#color=XLAT_TO_PURPLE),

                   Create(&LightRobe,#color=XLAT_TO_ORANGE),
                   Create(&LightRobe,#color=XLAT_TO_RED),
                   Create(&LightRobe,#color=XLAT_TO_SKY),                   
                   Create(&LightRobe,#color=XLAT_TO_GREEN),
                   Create(&LightRobe,#color=XLAT_TO_BLACK),
                   Create(&LightRobe,#color=XLAT_TO_YELLOW),
                   Create(&LightRobe,#color=XLAT_TO_PURPLE),

                   Create(&Tanktop,#color=XLAT_TO_ORANGE),
                   Create(&Tanktop,#color=XLAT_TO_RED),                   
                   Create(&Tanktop,#color=XLAT_TO_SKY),
                   Create(&Tanktop,#color=XLAT_TO_GREEN),
                   Create(&Tanktop,#color=XLAT_TO_BLACK),
                   Create(&Tanktop,#color=XLAT_TO_YELLOW),
                   Create(&Tanktop,#color=XLAT_TO_PURPLE)

                   ],$,$,$];
      return;
   }

   AddToConditionalList(obj=$,value=0)
   {
      propagate;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%



