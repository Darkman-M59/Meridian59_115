% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanApothecary is KocatanTown

constants:

   include blakston.khd

resources:

   KocatanApoth_name_rsc = "Zala ix'Mecya"
   kocatanapoth_icon_rsc = kcapoth.bgf
   kocatanapoth_desc_rsc = "Zala ist eine sich langsam bewegende Frau, die offensichtlich die irdischen Vergnügungen genießt. Sie hat eine weiche, heisere Stimme und nimmt lange Züge aus ihrer Tonpfeife."



   kocatanapoth_entry_welcome = "Friede, Liebe und Heiterkeit seien mit Dir, Freund."

   apoth_potion_riija = "Ja, ich habe den Trank des Vergessens. Trink ihn mit Vorsicht, denn seine Wirkung ist mächtig."
   kocatanapoth_herbs_sale = "Warum? Naja ich könnte meinen persönlichen Vorrat an %s%s auch mit Dir teilen."
   kocatanapoth_elderberry_sale = "Warum willst Du %s%s? Sie sehen nicht sehr wirksam aus?"
   kocatanapoth_bluemushroom_sale = "Wusstest Du wie %s%s nach einer arbeitsreichen Nacht wirken? Kauf ein paar davon."
   kocatanapoth_redmushroom_sale = "Ah, Du willst %s%s? Ich sehe schon, Du weißt, wie Du Dir selbst eine Freude bereiten kannst."

classvars:

   vrName = KocatanApoth_name_rsc
   vrIcon = KocatanApoth_icon_rsc
   vrDesc = KocatanApoth_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE | MOB_BUYER | MOB_COND_SELLER
   viOccupation = MOB_ROLE_APOTHECARY
   viGender = GENDER_FEMALE

properties:


messages:

   ObjectDesired(what=$)
   {
      if send(self,@IsObjectReagent,#what=what)
         AND NOT send(self,@IsObjectGem,#what=what)
      {
         return TRUE;
      }
      
      return FALSE;
   }

   AffectMood(what=$,why=$)
   {
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood+1);

         return;
      }
      
      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood-3);

         return;
      }

      if why = MOODMOD_SELL_ITEM
      {
         send(self,@SetMood,#new_mood=piMood+1);

         return;
      }

      if why = MOODMOD_ACCEPT_ITEM
      {
         if IsClass(what, &Herbs)
         {
            send(self,@SetMood,#new_mood=piMood+5);
         }
         else
         {
            send(self,@SetMood,#new_mood=piMood+1);
         }

         return;
      }

      return;
   }

   SetForSale()
   {
      plFor_sale = [ [ Create(&RainbowFern,#number=4),
                       Create(&UncutSeraphym,#number=1),
                       Create(&FireSand,#number=4),
                       Create(&DragonflyEye,#number=2),
                       Create(&Solagh,#number=4),
                       Create(&WebMoss,#number=4) ],
                      $, $, $ ];
      return;
   }

   UserEntered(who = $)
   {
      Send(poOwner,@SomeoneSaid,#what=self,#string=KocatanApoth_entry_welcome,
           #type=SAY_RESOURCE);
             
      if Send(who,@GetAction) = UA_HAPPY
      {
         send(self,@SetMood,#new_mood=piMood+3);
      }
      
      if Send(who,@GetAction) = UA_SAD
      {
         send(self,@SetMood,#new_mood=piMood-3);
      }
      
      propagate;
   }
   
 
   InitCondSale()
   {
      local oObj, oObj2, oObj3, oObj4, oObj5;
      
      oObj = Create(&ForgetPotionRiija);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,10000,apoth_potion_riija]);

      oObj2 = Create(&Herbs, #number=4);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj2,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj2,400,kocatanapoth_herbs_sale]);
           
      oObj3 = Create(&Elderberry, #number=3);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj3,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj3,500,kocatanapoth_elderberry_sale]);

      oObj4 = Create(&BlueMushroom, #number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj4,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj4,600,kocatanapoth_bluemushroom_sale]);

      oObj5 = Create(&RedMushroom, #number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj5,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj5,1000,kocatanapoth_redmushroom_sale]);

      return;
   }

   
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

