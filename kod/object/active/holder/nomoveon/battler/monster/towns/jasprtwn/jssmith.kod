% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
JasperBlacksmith is JasperTown

constants:

   include blakston.khd

resources:

   JasperBlacksmith_name_rsc = "Quintor"
   JasperBlacksmith_icon_rsc = jssmith.bgf
   JasperBlacksmith_desc_rsc = "Quintor ist weithin als der beste Waffenschmied im Land bekannt. Man kennt ihn auch als stillen, grüblerischen Mann, der meist für sich allein bleibt. Nimm es, wie Du willst."



   JsBla_entry_welcome = "Guten Tag, %s%s."

   JsBla_island_items = "Ich gebe mich nicht mit Schund aus Nerudit ab."

classvars:

   vrName = JasperBlacksmith_name_rsc
   vrIcon = JasperBlacksmith_icon_rsc
   vrDesc = JasperBlacksmith_desc_rsc
   viAttributes = MOB_NOFIGHT | MOB_SELLER | MOB_BUYER | MOB_RANDOM | MOB_NOMOVE | MOB_LISTEN | MOB_TEACHER | MOB_PERM_QUESTER
   viOccupation = MOB_ROLE_BLACKSMITH
   viMerchant_markup = MERCHANT_BARGAIN
   viQuestID = QST_ID_SCIMITAR_PROF

messages:

   Constructor()
   "We start a permanent quest for learning a skill" 
   {
      Send(self,@InitPermQuest);

      propagate;
   }

   AffectMood(what=$,why=$)
   {
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood-2);

         return;
      }

      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood+2);

         return;
      }

      if why = MOODMOD_SELL_ITEM
      {
         send(self,@SetMood,#new_mood=piMood+3);
      }

      return;
   }

   InitPermQuest()
   {
      Post(Send(SYS,@GetLibrary),@CreateQuest,#type=QST_PERM,#quester=self,
           #reward=[QST_REWARD_ID,viQuestID]);

      return;
   }

   CanAddSkill(who=$,num=0)
   "We require that a quest be done before one of the spells can be learned"
   {
      if (num = SKID_PROFICIENCY_SCIMITAR)
         AND NOT send(self,@HasDoneLearnQuest,#who=who)
      {
         return FALSE;
      }

      propagate;
   }

   ObjectDesired(what = $)
   {
      if Send(what,@CanBeGiventoNPC)
         AND (Send(Self,@IsObjectWeapon,#what=what)
              OR Send(Self,@IsObjectWearable,#what=what))
      {
         if IsClass(what,&NeruditeSword)
            OR IsClass(what,&NeruditeArmor)
            OR IsClass(what,&NeruditeBow)
         {
            send(poOwner,@SomeoneSaid,#what=self,#string=JsBla_island_items,#type=SAY_RESOURCE);
            return FALSE;
         }

         return TRUE;
      }
      
      return FALSE;
   }

   SetForSale()
   {
      plFor_sale = [
         [	Create(&Mace),
			   Create(&ShortSword),
			   Create(&GoldShield),
			   Create(&ScaleArmor),
			   Create(&Hammer),
			   Create(&LeatherArmor)
		   ],
         [SKID_PROFICIENCY_SCIMITAR], $ ];

      return;
   }

   UserEntered(who = $)
   {
      local iFlags;

      iFlags = send(who,@GetObjectFlags);

      if iFlags & PLAYER_OUTLAW
      {
         send(self,@SetMood,#new_mood=piMood+2);
      }

      if send(who,@GetHomeRoom) = RID_JAS_INN
      {
         send(self,@SetMood,#new_mood=piMood+1);
      }
      
      Post(poOwner,@SomeoneSaid,#what=self,#string=JsBla_entry_welcome,
           #type=SAY_RESOURCE,#parm1=Send(who,@GetDef),#parm2=Send(who,@GetName));

      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


