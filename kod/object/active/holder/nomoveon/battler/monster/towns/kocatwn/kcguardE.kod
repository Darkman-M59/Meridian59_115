% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanCaptainEvil is KocatanTown

constants:

   include blakston.khd
   BRIBE_AMOUNT= 1000

resources:

   KocatanCaptainEvil_name_rsc = "Xacal ko'Ixca"
   KocatanCaptainEvil_icon_rsc = kcguardE.bgf

   KocatanCaptainEvil_desc_rsc = "Als einer der höchstrangigen Offiziere Ko'catan Armee, Xacal ko'lxca hat sich in vielen Kampagnen bewährt. In Friedenszeiten es zu seinen Aufgaben, das Haupttorhaus während der Nachtstunden vor unbekannten Feinden zu schützen. Und da es zur Zeit recht friedlich ist, Xacal ko'lxas Pflicht ist, zu definieren, wer diese Feinde sind. Er ist allgemein wegen seiner Härte und Durchsetzungskraft respektiert."






   evilguard_Pass = "Du bist ganz allein. "
   evilguard_winknudge = "Weißt du, es ist schon komisch, wie dieses Geld aus Deiner Börse in meine Hände fällt."
   evilguard_winknudge_action = "~kXacal winkt und gibt Dir einen Stups."
   
   evilguard_no_money = "Wenn Du Dich dem Tor näherst, musst Du bereit sein zu handeln. Du bist pleite und stiehlst mir meine Zeit. "
   evilguard_not_enough_money = "Nicht genug."

classvars:

   vrName = KocatanCaptainEvil_name_rsc
   vrIcon = KocatanCaptainEvil_icon_rsc
   vrDesc = KocatanCaptainEvil_desc_rsc

   viMerchant_markup = MERCHANT_RIPOFF

   viAttributes = MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN  | MOB_RECEIVE | MOB_NOMOVE | MOB_FULL_TALK
   viOccupation = MOB_ROLE_SERGEANT

properties:

   plBribes = $
   
messages:

   Constructor()
   {
       plWantedItems = [&Money];
       propagate;
   }

   NewOwner()
   {
      plBribes = $;
      propagate;
   }

   AllowContrabandToPass(What=$)
   {
      local i;
      for i in plBribes
      {
	 if i = what
	 {
	    send(self,@SayToOne,#target=what,#message_rsc=evilguard_Pass);
	    plBribes = dellistelem(plBribes,i);
	    return TRUE;
	 }
      }
      return FALSE;
   }

   Delete()
   {
      plBribes = $;
      propagate;
   }

   GotWantedItem(obj = $, who = $)
   {
      if send(self,@checkForBribe,#obj=obj,#who=who)
      {  return;  }

      propagate;
      
   }

   CheckForBribe(who=$, obj = 0)
   {
      local oMoney, oBank, amount;

      if not isClass(obj,&Money)  { return false; }
      amount = send(obj,@GetNumber);

      if amount <= 0 { return false; }

      oMoney = Send(who,@GetMoneyObject);
      if oMoney = $ 
      {
         Post(poOwner,@SomeoneSaid,#what=self,#string=Evilguard_no_money,
	   #type=SAY_RESOURCE);
	 return false;
      }
      if send(oMoney,@GetValue) < amount
      {
         Post(poOwner,@SomeoneSaid,#what=self,#string=EvilGuard_no_money,
	   #type=SAY_RESOURCE,#parm1=Send(oMoney,@GetValue));
	 return false;
      }
      if amount < BRIBE_AMOUNT
      {
         Post(poOwner,@SomeoneSaid,#what=self,#string=EvilGuard_not_enough_money,
	   #type=SAY_RESOURCE,#parm1=Send(oMoney,@GetValue));
	 return true;      
      }            
      Send(oMoney,@SubtractNumber,#number=amount);      
      plBribes = cons(who,plBribes);
      post(self,@SayToOne,#target=who,#message_rsc=evilguard_winknudge);
      post(who,@msgsenduser,#message_rsc=evilguard_winknudge_action);
      return true;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

