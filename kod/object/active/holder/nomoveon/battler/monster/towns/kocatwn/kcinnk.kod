% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanInnkeeper is KocatanTown

constants:

   include blakston.khd

resources:

   Kocataninnkeeper_name_rsc = "Moxal ix'Hanak"
   kocataninnkeeper_icon_rsc = kcinnk.bgf
   kocataninnkeeper_desc_rsc = "Von Natur aus unternehmerisch veranlagt, hält Moxal ix'Hanak stets Ausschau nach einer neuen hochriskanten Investition. Er grüßt dich mit einem selbstbewussten Lächeln und einem geheimnissvollen Glimmen in seinen Augen."




   kocataninnkeeper_entry_welcome = "Willkommen, Patron. Ich hoffe, Du hast einen angenehmen Aufenthalt."
   kocinn_winknudge = "Danke! Ich werde Dich bestimmt wissen lassen, was dabei herauskommt!"
   kocataninnkeeper_unwanted_keep = "~k%s%s erzählt Dir, \"Danke. Du wirst es nicht bereuen, dass Du es mir geliehen hast.~n~k\""

   KocatanInkeeper_purplemushroom_sale = "Ah! Du möchtest %s%s? Ich habe sie frisch vom Festland importiert. Sie sollen Stärke verleihen, wenn man 3000 davon isst."

classvars:

   vrName = Kocataninnkeeper_name_rsc
   vrIcon = Kocataninnkeeper_icon_rsc
   vrDesc = Kocataninnkeeper_desc_rsc

   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE | MOB_COND_SELLER
   viOccupation = MOB_ROLE_INNKEEPER

   vrUnwanted_keep = kocataninnkeeper_unwanted_keep

properties:   

messages:

   AffectMood(why = $, what = $)
   {
      if why = MOODMOD_SELL_ITEM
      {
         send(self,@SetMood,#new_mood=piMood + 1);
      }
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood + 3);
         return;
      }
      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood - 3);
         return;
      }
      return;
   }

   Constructor()
   {
       plWantedItems = [&Money];
       propagate;
   }

   GotWantedItem(obj = $, who = $)
   {
      if send(self,@CheckForScheme,#obj=obj,#who=who)
      {
         return;
      }
      
      propagate;     
   }

   CheckForScheme(who=$, obj = 0)
   {
      local oMoney, oBank, amount;

      if not isClass(obj,&Money)
      {
         return FALSE;
      }
      
      amount = send(obj,@GetNumber);

      if amount <= 0
      {
         return FALSE;
      }

      oMoney = Send(who,@GetMoneyObject);
      
      if oMoney = $ 
      {
         return FALSE;
      }
      
      if send(oMoney,@GetValue) < amount
      {
         return FALSE;
      }
      
      Send(oMoney,@SubtractNumber,#number=amount);            
      post(self,@SayToOne,#target=who,#message_rsc=kocinn_winknudge);

      return TRUE;
   }

   SetForSale()
   {
      plFor_sale = [ [                       
                       Create(&Meatpie,#number=4),
                       Create(&Bread,#number=4),
                       Create(&Cheese,#number=6)
	               ],
                      $, $, $ ];
      return;
   }

   UserEntered(who = $)
   {
      if Send(who,@GetAction) = UA_HAPPY
      {
         send(self,@SetMood,#new_mood=piMood + 2);
      }
      if Send(who,@GetAction) = UA_SAD
      {
         send(self,@SetMood,#new_mood=piMood + 1);
      }
      if Send(who,@GetAction) = UA_WRY
      {
         send(self,@SetMood,#new_mood=piMood - 2);
      }
      
      Send(poOwner,@SomeoneSaid,#what=self,#string=Kocataninnkeeper_entry_welcome,
             #type = SAY_RESOURCE);
             
      propagate;
   }

   InitCondSale()
   {
      local oObj;
      
      oObj = Create(&PurpleMushroom,#number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,1250,KocatanInkeeper_purplemushroom_sale]);
           
      return;
   }
   
   
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

