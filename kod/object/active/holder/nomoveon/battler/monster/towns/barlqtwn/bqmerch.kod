% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
BarloqueMerchant is BarloqueTown

constants:

   include blakston.khd

resources:

   barloque_merch_name_rsc = "Herbut"
   barloque_merch_icon_rsc = bqmerch.bgf
   barloque_merch_desc_rsc = "Sich für einen Kenner der feinen Lebensart haltend, ist Herbut doch nicht mehr als ein Snob alter Schule. Er verachtet fast alles bis auf das Königliche. Ihm wird niemals der Gesprächsstoff über die negativen Seiten der anderen Städte und über das einfache Leben der Reisenden zwischen ihnen ausgehen."





   Bqmerch_unwanted_give = "~k%s%s sagt zu Dir, \"Warum bietet Ihr mir solch gewöhnlichen Tand an?! Ich habe Diener, die zum Markt gehen, um mir solche Sachen zu besorgen.~n~k\""

classvars:
   vrName = barloque_merch_name_rsc
   vrIcon = barloque_merch_icon_rsc
   vrDesc = barloque_merch_desc_rsc

   viMerchant_markup = MERCHANT_DISCOUNT

   viAttributes = MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN | MOB_BUYER | MOB_SELLER | MOB_NOMOVE | MOB_LAWFUL
   viOccupation = MOB_ROLE_MERCHANT

   vrUnwanted_give = Bqmerch_unwanted_give

properties:
   
messages:

   UserEntered( who = $ )
   {
      local iFlags;
      iFlags = send( who, @GetObjectFlags );
      if iFlags & PLAYER_PK
      {
	 send( self, @SetMood, #new_mood = piMood - 3 );
      }
      if iFlags & PLAYER_OUTLAW
      {
	 send( self, @SetMood, #new_mood = piMood - 2 );
      }
      if( send( who, @GetHomeRoom ) = RID_BAR_INN )
      {
	 send( self, @SetMood, #new_mood = piMood + 1 );
      }
      if( send( who, @GetHomeRoom ) = RID_TOS_INN )
      {
	 send( self, @SetMood, #new_mood = piMood - 1 );
      }
      propagate;
   }

   AffectMood( why = $, what = $ )
   {
      if( why = MOODMOD_ACCEPT_ITEM )
      {
	 if( Send(self,@IsObjectGem,#what=what) )
	 {
	    send( self, @SetMood, #new_mood = piMood + 1 );
	 }
	 return;
      }
      if( why = MOODMOD_FACTION_CHANGE )
      {
	 if( what = FACTION_DUKE )
	 {
	    send( self, @SetMood, #new_mood = piMood - 1 );
	    return;
	 }
	 if( what = FACTION_PRINCESS )
	 {
	    send( self, @SetMood, #new_mood = piMood + 1 );
	    return;
	 }
      }
      if( why = MOODMOD_DAWN )
      {
	 send( self, @SetMood, #new_mood = piMood + 1 );
	 return;
      }
      if( why = MOODMOD_DUSK )
      {
	 send( self, @SetMood, #new_mood = piMood - 1 );
	 return;
      }
      if( why = MOODMOD_SELL_ITEM )
      {
	 if( Send(self,@IsObjectGem,#what=what) )
	 {
   	    send( self, @SetMood, #new_mood = piMood + 2 );
	 }
	 return;
      }

      return;
   }

   ObjectDesired(what=$)
   {
      if isClass(what,&NumberItem) and
         send(what,@GetNumber) > 25
      {
         return FALSE;
      }

      if send(self,@IsObjectGem,#what=what) or
         send(self,@IsObjectSundry,#what=what)
      {
	 return TRUE;
      }

      return FALSE;
   }

   SetForSale()
   {
      plFor_sale = [ [ Create(&Diamond,#number=1),
                       Create(&Torch),                       
                       Create(&Sapphire,#number=1),
                       Create(&Emerald,#number=1) ],
                   $,$];
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

