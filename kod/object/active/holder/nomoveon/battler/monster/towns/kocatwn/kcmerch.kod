% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
kocatanTradeMaster is KocatanTown

constants:

   include blakston.khd

resources:

   kocatanTradeMaster_name_rsc = "Wulfgang zax'Ak"
   kocatanTradeMaster_icon_rsc = kcmerch.bgf
   kocatanTradeMaster_desc_rsc = "Ein bemerkenswerter Mann, der ein hartes Leben hinter sich hat. Wulfgang wurde auf dem Festland geboren und ist in einer verfolgten  Geheimgesellschaft der letzten Riijaschule aufgezogen worden. Außer, dass er irgendwie auf diese Insel geflohen ist und hier seinen Handelsposten etabliert hat, weiß man wenig über seine Vergangenheit."




   
   Wolfgang_lute = "Eine Laute ... eine Laute ... Ich glaube, ich hab' eine davon irgendwo ..."
   Wolfgang_potion_jala = "Ja, ich habe eine davon. Natürlich habe ich keine Verwendung dafür..."
   Wolfgang_entrootberry_sale = "Du fragst nach %s%s? Nimm sie. Sie sind nur eine bittere Erinnerung für mich für das, was ich hinter mir zurückließ."
   Wolfgang_fairywing_sale = "Du möchtest %s%s? Nimm ruhig. Ich habe keine Verwendung mehr dafür."
   Wolfgang_mushroom_sale = "Ah, Du möchtest %s%s kaufen? Ich erinnere mich daran, wie ich sie als Kind rund um meine Heimatstadt gesammelt habe."

   Wolfgang_Nerudite_bought = "Wunderbar! Das wird mir von den... ähem... Kunden einen guten Preis einbringen."
   Wolfgang_Nerudite_cant_accept_money = "Ich würde Dir gerne dieses feine Stück Erz aus Deinen Händen abnehmen, aber ich fürchte, Du schleppst zu viel mit dir herum, um die Bezahlung annehmen zu können."

   
classvars:

   vrName = kocatanTradeMaster_name_rsc
   vrIcon = kocatanTradeMaster_icon_rsc
   vrDesc = kocatanTradeMaster_desc_rsc

   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE | MOB_BUYER | MOB_COND_SELLER
   viOccupation = MOB_ROLE_MERCHANT

properties:
   
messages:

   ObjectDesired(what=$)
   {
      return TRUE;
   }

   AffectMood(what=$,why=$)
   {
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood - 2);

         return;
      }

      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood+2);

         return;
      }

      if why = MOODMOD_SELL_ITEM
      {
         send(self,@SetMood,#new_mood=piMood+2);

         return;
      }

      if why = MOODMOD_ACCEPT_ITEM
      {
         if IsClass(what,&NeruditeOreChunk)
         {
            send(self,@SetMood,#new_mood=piMood+4);
         }
         else
         {
            send(self,@SetMood,#new_mood=piMood+2);
         }

         return;
      }

      return;
   }

   UserEntered(who = $)
   {
      local iFlags;

      if send(who, @GetHomeRoom) = RID_MAR_INN
      {
         send(self,@SetMood,#new_mood=piMood+2);
      }
      
      if send(who, @GetFaction) = FACTION_NEUTRAL
      {
         send(self,@SetMood,#new_mood=piMood+2);
      }
      else
      {
         send(self,@SetMood,#new_mood=piMood-3);
      }
      
      iFlags = send(who,@GetObjectFlags);
      if iFlags & PLAYER_PK
      {
         send(self,@SetMood,#new_mood=piMood+2);
      }
      
      if iFlags & PLAYER_OUTLAW
      {
         send(self,@SetMood,#new_mood=piMood+2);
      }
      else
      {
         if NOT (iFlags & PLAYER_PK)
         {
            send(self,@SetMood,#new_mood=piMood-2);
         }
      }
      
      propagate;
   }

   SetForSale()
   {
      plFor_sale = [ [ Create(&KocatanMug,#number=4),
                       Create(&WineGoblet,#number=4),
                       Create(&Bread,#number=4),
                       Create(&Apple),
                       Create(&Mace),
                       Create(&LeatherArmor),
                       Create(&SilverArrow),
                       Create(&NeruditeArrow),
                       Create(&Torch),
                       Create(&MetalShield) ],
                     $,
                     [ SID_BLOOD_INHERITANCE ],
                     $ ];
      return;
   }
   
   InitCondSale()
   {
      local oObj, oObj2, oObj3, oObj4, oObj5;
      
      oObj=Create(&Lute);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,10000,Wolfgang_lute]);
           
      oObj2 = Create(&ForgetPotionJala);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj2,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj2,20000,Wolfgang_potion_jala]);

      oObj3 = Create(&EntrootBerry, #number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj3,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj3,400,Wolfgang_entrootberry_sale]);

      oObj4 = Create(&FairyWing, #number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj4,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj4,300,Wolfgang_fairywing_sale]);

      oObj5 = Create(&Mushroom, #number=2);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj5,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj5,300,Wolfgang_mushroom_sale]);

      return;

   }
   
   CheckWhyWanted(obj=$,who=$)
   {
      local oMoney;

      if isClass(obj,&NeruditeOreChunk)
      {
         oMoney = Create(&Money,#number=(Send(obj,@GetValue)+1300));
         if send(who,@ReqNewHold,#what=oMoney)
         {
            Send(who,@NewHold,#what=oMoney);
            Post(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
                #string=Wolfgang_Nerudite_bought);

            return TRUE;
         }
         else
         {
            Send(oMoney,@Delete);
            Post(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
                #string=Wolfgang_Nerudite_cant_accept_money);

            return FALSE;
         }

         return TRUE;
      }

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

