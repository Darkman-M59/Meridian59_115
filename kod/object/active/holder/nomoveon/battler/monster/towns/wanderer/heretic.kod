% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Heretic is Wanderer

constants:

   include blakston.khd
   ANIM_SPEED_FAST = 100
   ANIM_SPEED_SLOW = 150
   FRAME_DANCE_START = 1
   FRAME_DANCE_END = 8

resources:

   Heretic_name_rsc = "Miriana"
   Heretic_icon_rsc = wnhereti.bgf
   Heretic_possessed_icon_rsc = wnheret2.bgf
   Heretic_desc_rsc = "Ausgestoßen von der religiösen Gemeinde wegen Ihrer blasphemischen Ansichten über die Götter, tanzt Miriana alleine, weit weg von aller Zivilisation. Einige nennen sie eine Ketzerin, andere ein Kind, dennoch, etwas scheint sehr gefährlich an diesem seltsamen Mädchen."





   heretic_inexperienced = "Ich werde diese Künste niemanden lehren, der so schwach ist."
   Heretic_already_have_spell = "Dummerchen! Du beherrschst den %s-Spruch bereits."

classvars:

   vrName = Heretic_name_rsc
   vrDesc = Heretic_desc_rsc

   viAttributes = MOB_NOFIGHT | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE | MOB_TEACHER
   viOccupation = MOB_ROLE_PRIESTESS
   viGender = GENDER_FEMALE

properties:

   vrIcon = Heretic_icon_rsc
   piAnimation = ANIM_SPEED_FAST

messages:

   UserEntered(who = $)
   {
      local iAction;

      iAction = send(who,@GetAction);
      
      if iAction = UA_SAD
      {
         send(self,@SetMood,#new_mood=piMood+1);
      }

      if iAction = UA_HAPPY
      {
         send(self,@SetMood,#new_mood=piMood-1);
      }

      if iAction = UA_WRY
      {
         send(self,@SetMood,#new_mood=piMood+2);
      }

      propagate;
   }

   CreateDestinationList()
   {
      plDestinations = [ [RID_A1, 24, 46], [RID_F7, 4, 16], [RID_A6, 20, 32],
                         [RID_I7, 32, 7]  ];
      return;
   }
   
   SetForSale()
   {
      plFor_Sale = [$,$,[SID_FIREWALL, SID_MARTYRS_BATTLEGROUND, SID_BRAMBLEWALL, SID_BOND,
                         SID_SILENCE, SID_RING_OF_FLAMES ]];
      return;
   }

   SetPossessed(bValue=FALSE)
   {
      if bValue
      {
         vrIcon = Heretic_possessed_icon_rsc;
         piAnimation = ANIM_SPEED_SLOW;
      }
      else
      {
         vrIcon = Heretic_icon_rsc;
         piAnimation = ANIM_SPEED_FAST;
      }

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,piAnimation,2,FRAME_DANCE_START,2,FRAME_DANCE_END);

      return;
   }
   
   SendAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE);
      AddPacket(4,piAnimation,2,FRAME_DANCE_START,2,FRAME_DANCE_END);

      return;
   }

   CanDoTeach(who=$,sid=0)
   {
      if NOT send(self,@SpecialTeachChecks,#report=TRUE,#who=who,#num=sid)
      {
         return FALSE;
      }

      propagate;
   }
   
   CanAddSpell(who=$,num=0, report = FALSE)
   {
      if NOT send(self,@SpecialTeachChecks,#report=report,#who=who,#num=num)
      {
         return FALSE;
      }

      propagate;
   }
   
   SpecialTeachChecks(who=$,num=0, report = FALSE)
   {         
      if who = $ or num = 0
      { DEBUG("Called canaddspell with bad parameters!"); return FALSE; }
   
      if send(who,@HasSpell,#num=num) 
      {
         if report
         { 
            post(self,@SayToOne,#target=who,#message_rsc=Heretic_already_have_spell,
                 #parm1=send(send(SYS,@FindSpellByNum,#num=num),@GetName)); 
         }

         return FALSE;
      }

      %% only players who are killable may learn 
      if (not send(who,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE)
         AND (num = SID_BRAMBLEWALL OR num = SID_FIREWALL OR num = SID_RING_OF_FLAMES))
      { 
         if report
         { 
            post(self,@SayToOne,#target=who,#message_rsc=heretic_inexperienced);
         }

         return FALSE;
      }

      return TRUE;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

