% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanBowMaker is KocatanTown

constants:

   include blakston.khd

resources:

   KocatanBowMaker_name_rsc = "Hanla zax'Ta"
   KocatanBowMaker_icon_rsc = kcbowmak.bgf
   KocatanBowMaker_desc_rsc = "Diese schickliche, junge Frau erscheint neuen Gesichtern kalt und schroff. Nichtsdestotrotz ist es offensichtlich, dass ihre wahre Leidenschaft der Herstellung schöner Bogen gilt."


   KocatanBowMaker_KriipaClaw_sale = "Ich habe die Klauen mehrerer Kriipa, die ich im Wald tötete. Der Kriipa ist wirklichein scheues Tier, wie Du sicherlich weißt, und ein Jäger, der ihre Klauen ergattern will, muss dafür mit seinem Blut, das während des Kampfes vergossen wird, bezahlen. Deshalb wirst Du verstehen, dass ich einen hohen Preis verlange."



   KocatanBowMaker_unwanted_give = "~k%s%s sagt zu Dir, \"Ich habe genug von diesen Vorräten, um uns über den Sommer zu bringen.~n~k\""

   
classvars:

   vrName = KocatanBowMaker_name_rsc
   vrIcon = KocatanBowMaker_icon_rsc
   vrDesc = KocatanBowMaker_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE
   viOccupation = MOB_ROLE_BLACKSMITH

   viGender = GENDER_FEMALE

   vrUnwanted_give = KocatanBowMaker_unwanted_give


properties:
   
messages:

   AffectMood(why = $, what = $)
   {
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood + 2);

         return;
      }

      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood - 2);

         return;
      }

      if why = MOODMOD_SELL_ITEM
      {
         if IsClass(what,&LongBow)
         {
            send(self,@SetMood,#new_mood=piMood - 2);
         }

         return;
      }

      return;
   }

   UserEntered(who = $)
   {
      if Send(who,@GetGender) = GENDER_MALE
      {
         send(self,@SetMood,#new_mood=piMood - 1);
      }

      if Send(who,@GetGender) = GENDER_FEMALE
      {
         send(self,@SetMood,#new_mood=piMood + 2);
      }

      return;
   }

   SetForSale()
   {
      plFor_sale = [ [ Create(&LongBow),
                       Create(&Arrow)
                     ],
                     $,$,$
                   ];
      return;
   }

   InitCondSale()
   {
      local oObj;

      oObj=Create(&KriipaClaw,#number=4);
      Send(Send(SYS,@GetLibrary),@AddToMobLib,#mob=self,
           #triggers=[Send(oObj,@GetName)],
           #action=[LIBACT_CONDITIONAL,oObj,250,KocatanBowMaker_KriipaClaw_sale]);

      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%



