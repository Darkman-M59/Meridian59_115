% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
KocatanBartender is KocatanTown

constants:

   include blakston.khd

resources:

   KocatanBartender_name_rsc = "Oxal ix'Tokak"
   kocatanBartender_icon_rsc = kcbart.bgf
   kocatanBartender_desc_rsc = "Nachdem er seinen rechten Arm in der Schlacht bei Balo na'te Din verloren hatte, wurde Oxal aus dem Dienst in der Armee von Ko'catan entlassen und arbeitet jetzt in der Kneipe zum bellenden Monster. Es ist eine große Ehre, als Veteran ausgewählt zu werden, diese der Armee gehörende Kneipe zu führen. Doch Oxal geht seiner Arbeit offensichtlich mit wenig Begeisterung nach."




   kocatanBartender_entry_welcome = "Was willst Du?"
   
classvars:

   vrName = KocatanBartender_name_rsc
   vrIcon = KocatanBartender_icon_rsc
   vrDesc = KocatanBartender_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE

   viAttributes = MOB_NOFIGHT | MOB_SELLER | MOB_RANDOM | MOB_LISTEN | MOB_NOMOVE | MOB_RECEIVE | MOB_BUYER
   viOccupation = MOB_ROLE_BARTENDER

properties:
   
messages:

   ObjectDesired(what=$)
   {
      if isClass(what,&ShortSword)
      {
         return TRUE;
      }
      
      return FALSE;
   }


   AffectMood(what=$,why=$)
   {
      if why = MOODMOD_DAWN
      {
         send(self,@SetMood,#new_mood=piMood - 3);
         return;
      }
      if why = MOODMOD_DUSK
      {
         send(self,@SetMood,#new_mood=piMood + 3);
         return;
      }
      if (why = MOODMOD_SELL_ITEM)
      {
         if isClass(what,&food) 
         {
            if send(what, @IsAlcohol) 
            {
               send(self,@SetMood,#new_mood=piMood + 1);
            }
         }
      }
      
      return;
   }

   SetForSale()
   {
      plFor_sale = [ [
                       Create(&KocatanMug,#number=4),
                       Create(&Bread,#number=4) ],
                      $, $ ];
      return;
   }

   UserEntered(who = $)
   {
      if send(who, @GetGender) = GENDER_MALE
      {
         send(self,@SetMood,#new_mood=piMood + 1);
      }
      
      if Send(who,@GetAction) = UA_WRY
      {
         send(self,@SetMood,#new_mood=piMood + 1);
      }
      
      Send(poOwner,@SomeoneSaid,#what=self,#string=KocatanBartender_entry_welcome,
             #type = SAY_RESOURCE);
             
      propagate;
   }
   
end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

