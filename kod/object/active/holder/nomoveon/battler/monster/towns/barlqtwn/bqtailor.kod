% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Barloquetailor is BarloqueTown

constants:

   include blakston.khd
   
resources:

   Barloquetailor_name_rsc = "Madelia"
   Barloquetailor_icon_rsc = bqtailor.bgf
   Barloquetailor_desc_rsc = "Daran gewöhnt, für den raffinierten Geschmack des Adels zu arbeiten, führt Madelia ihr Handwerk mit großer Sorgfalt aus. Trotzdem wünscht sie sich, dass sie mehr Zeit einem einzigen Kleidungsstück eines mächtigeren und geschätzten Mitglied des Hofes widmen könnte. Madelias anderes Hobby ist es, mit den Herzen der Männer zu spielen, und zur Zeit behält sie mehrere Beziehungen am Kochen."




   Batail_entry_welcome = "Seid gesegnet, %s%s. Was für schreckliche Sachen tragt Ihr da?"
   Batail_unwanted_give = "~k%s%s sagt zu Dir, \"Ihr solltet Euch einen ordentlichen Kaufmann aussuchen, der Euch Waren verkauft.~n~k\""


classvars:

   vrName = Barloquetailor_name_rsc
   vrIcon = Barloquetailor_icon_rsc
   vrDesc = Barloquetailor_desc_rsc
   viMerchant_markup = MERCHANT_EXPENSIVE
   viAttributes = MOB_NOFIGHT | MOB_RANDOM | MOB_NOMOVE | MOB_LISTEN | MOB_RECEIVE | MOB_SELLER | MOB_LAWFUL
   viOccupation = MOB_ROLE_TAILOR
   viGender = GENDER_FEMALE

   vrUnwanted_give = Batail_unwanted_give

properties:
   
messages:

   AffectMood( why = $, what = $ )
   {
      if( why = MOODMOD_FACTION_CHANGE )
      {
	 if( what = FACTION_DUKE )
	 {
	    send( self, @SetMood, #new_mood = piMood - 3 );
	    return;
	 }
	 if( what = FACTION_PRINCESS )
	 {
	    send( self, @SetMood, #new_mood = piMood + 3 );
	    return;
	 }
      }
      if( why = MOODMOD_DAWN )
      {
	 send( self, @SetMood, #new_mood = piMood + 2 );
	 return;
      }
      if( why = MOODMOD_DUSK )
      {
	 send( self, @SetMood, #new_mood = piMood - 2 );
	 return;
      }
      if( why = MOODMOD_SELL_ITEM )
      {
	 send( self, @SetMood, #new_mood = piMood + 2 );
	 return;
      }
      return;
   }

   UserEntered(who = $)
   {
      Post(poOwner,@SomeoneSaid,#what = self, #string=Batail_entry_welcome,
         #type = SAY_RESOURCE, 
         #parm1=Send(who,@GetDef),#parm2=Send(who,@GetName));
      if( send( who, @GetHomeRoom ) = RID_BAR_INN )
      {
	 send( self, @SetMood, #new_mood = piMood + 2 );
      }
      if( send( who, @GetHomeRoom ) = RID_TOS_INN )
      {
	 send( self, @SetMood, #new_mood = piMood - 2 );
      }
      if( send( who, @GetHomeRoom ) = RID_JAS_INN )
      {
	 send( self, @SetMood, #new_mood = piMood - 1 );
      }
      if( send( who, @GetGender ) = GENDER_MALE )
      {
	 send( self, @SetMood, #new_mood = piMood + 2 );
      }
      if( send( who, @GetGender ) = GENDER_FEMALE )
      {
	 send( self, @SetMood, #new_mood = piMood - 2 );
      }
      return;
   }

   SetForSale()
   {
      %% Madelia favors more regal clothes:  deep, luxurious colors and 
      %% full length robes and shirts.
      plFor_sale=[[
      		   Create(&PantsC,#translation=PT_GRAY_TO_DGREEN),
		   Create(&PantsC,#translation=PT_GRAY_TO_KBGREEN),
		   Create(&PantsC,#translation=PT_GRAY_TO_LSKY),
		   Create(&PantsC,#translation=PT_GRAY_TO_DBLUE),
		   Create(&PantsC,#translation=PT_GRAY_TO_KPURPLE),
		   Create(&PantsC,#translation=PT_GRAY_TO_KGOLD),
		   Create(&PantsC,#translation=PT_GRAY_TO_KBLUE),
		   Create(&PantsC,#translation=PT_GRAY_TO_KBBLUE),
		   Create(&PantsC,#translation=PT_GRAY_TO_KRED),
		   Create(&PantsC,#translation=PT_GRAY_TO_KORANGE),
		   
		   Create(&PantsA,#color=XLAT_TO_BLUE),
		   Create(&PantsA,#color=XLAT_TO_PURPLE),
		   Create(&PantsA,#color=XLAT_TO_SKY),
		   Create(&PantsA,#color=XLAT_TO_DGREEN),		   

		   Create(&Shirt,#color=XLAT_TO_SKY),
		   Create(&Shirt,#color=XLAT_TO_PURPLE),
		   Create(&Shirt,#color=XLAT_TO_DGREEN),
		   Create(&Shirt,#color=XLAT_TO_BLUE),

		   Create(&Robe,#color=XLAT_TO_PURPLE),
		   Create(&Robe,#color=XLAT_TO_GRAY),
		   Create(&Robe,#color=XLAT_TO_BLACK),
		   Create(&Robe,#color=XLAT_TO_BLUE),
		   Create(&Robe,#color=XLAT_TO_RED),
		   Create(&Robe,#color=XLAT_TO_SKIN4),
		   Create(&Robe,#color=XLAT_TO_DGREEN)

%%%% These ended up not looking too good.
%		   Create(&RoyalShirt,#color1=XLAT_TO_RED,#color2=XLAT_TO_BLUE),
%		   Create(&RoyalShirt,#color1=XLAT_TO_BLUE,#color2=XLAT_TO_PURPLE),
%		   Create(&RoyalShirt,#color1=XLAT_TO_GREEN,#color2=XLAT_TO_BLUE),
%		   Create(&RoyalShirt,#color1=XLAT_TO_BLUE,#color2=XLAT_TO_RED)

                   ],$,$];
      return;
   }

end

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


