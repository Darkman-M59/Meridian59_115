% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DuskRat is Monster

constants:

   include blakston.khd

   POISON_CHANCE = 6
   POISON_DURATION = 30000   %%% in milliseconds
   POISON_LOSSRATE = 10000    %%% in health points * 10^-4 / second

resources:

   duskrat_koc_name_rsc = "teotnapyijoa"
   duskrat_name_rsc = "Die schwarze Ratte"
   duskrat_icon_rsc = rat.bgf

   duskrat_desc_rsc = "Die Luft um die Ratte ist dunkel und erfüllt mit Bösem. Der Geruch von Seuchen und Tod liegt auf ihrem stinkendem Atem als sie auf ihr Ziel zuschnellt. Die Herkunft dieser Abscheulichkeit ist unbekannt. Das einzige wirkliche Wissen besteht über das tödliche Gift, welches sie mit ihren Bissen überträgt."





   duskrat_poisoned_add_rsc = "Als das Gift in deinen Kopf wandert, fühlst du dich etwas leichtsinnig. Der Raum um dich herum beginnt sich zu drehen."



   duskrat_dead_icon_rsc = ratX.bgf
   duskrat_dead_name_rsc = "Eine tote schwarze Ratte"

   duskrat_sound_hit = rat_atkh.wav
   duskrat_sound_miss = rat_atkm.wav
   duskrat_sound_death = rat_dth.wav
   duskrat_sound_aware = rat_awr.wav

classvars:

   vrKocName = duskrat_koc_name_rsc
   vrName = duskrat_name_rsc
   vrIcon = duskrat_icon_rsc
   vrDesc = duskrat_desc_rsc
   vrDead_icon = duskrat_dead_icon_rsc
   vrDead_name = duskrat_dead_name_rsc
   viDead_drawfx = DRAWFX_BLACK

   viTreasure_type = TID_RAT

   viSpeed = SPEED_VERY_FAST
   viAttack_type = ATCK_WEAP_CLAW
   viAttributes = 0
   viLevel = 50
   viDifficulty = 6
   viKarma = -40
   viDefault_behavior = AI_FIGHT_AGGRESSIVE | AI_FIGHT_SWITCHALOT | AI_MOVE_REGROUP
   viLight_Penalty = 30

   vrSound_hit = duskrat_sound_hit
   vrSound_miss = duskrat_sound_miss
   vrSound_aware = duskrat_sound_aware
   vrSound_death = duskrat_sound_death

properties:

   piAnimation = ANIM_NONE
   piDrawfx = DRAWFX_BLACK

messages:

   Constructed()
   {
      plResistances = [ [-30, -ATCK_SPELL_FIRE ], 
                        [ 10, ATCK_WEAP_BLUDGEON ] ,
                        [ 20, ATCK_WEAP_SLASH ]
                      ];

      propagate;
   }

   MonsterAttack(what = $)
   {
      Send(self,@DoSlash);

      return;
   }

   DoSlash()
   {
      piAnimation = ANIM_ATTACK;
      Send(poOwner,@SomethingChanged,#what=self);
      piAnimation = ANIM_NONE;

      return;
   }

   SendLookAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,125,2,2,2,6);

      return;
   }

   SendMoveAnimation()
   {
      AddPacket(1,ANIMATE_CYCLE,4,75,2,2,2,6);

      return;
   }

   SendAnimation()
   {
      if piAnimation = ANIM_ATTACK
      {
         AddPacket(1,ANIMATE_ONCE,4,100,2,7,2,11,2,1);

         return;
      }

      propagate;
   }
 
   NewOwner(what = $)
   "Make room darker when present, about 2X darker than a ShadowBeast"
   {
      %New room
      if what <> $
      {
         Send(what,@AddBaseLight,#amount=-viLight_Penalty);
      }
      
      %Old room
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }
      
      propagate;
   }

   HitSideEffect( what = $ )
   {
      local oSpell;

      if Random(1,POISON_CHANCE) = 1
      {
         oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
         Send(oSpell,@MakePoisoned,#who=what,
              #lossrate=POISON_LOSSRATE,#duration=POISON_DURATION);
         Send(what,@EffectSendUserDuration,#effect=EFFECT_WAVER,#duration=10000);
         Send(what,@MsgSendUser,#message_rsc=duskrat_poisoned_add_rsc);
      }

      return;
   }

   IsAlly(target = $)
   {
      % Qor Characters?
      return FALSE;
   }
   
   Killed()
   {
      % old room
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }
      
      propagate;
   }

   Delete()
   {
      if poOwner <> $
      {
         Send(poOwner,@AddBaseLight,#amount=viLight_Penalty);
      }
      
      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

