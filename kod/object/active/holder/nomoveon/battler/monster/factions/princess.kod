% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
PrincessLiege is Factions

constants:

   include blakston.khd

resources:

   princess_name_rsc = "Prinzessin Kateriina"
   princess_icon_rsc = princess.bgf
   princess_desc_rsc = "Das Alter machte Prinzessin Kateriina stärker und selbstbewußter, aber es änderte nur wenig an ihrer Naivität. Sie entwickelte eine große Verachtung für den Herzog und alle, die sich in politischen Intrigen engagieren. Politische Intrigen sind unter ihrer Würde, weil sie darauf vertraut, dass man sie akzeptiert, wenn sie weise und gerecht regiert. Die Magie fasziniert sie weiterhin und manchmal träumt sie davon, wegzulaufen, um die geheimnisvollen Künste zu studieren."

   princess_token_delivered_rsc = "Ahhh. Ich werde dafür sorgen, dass dies ans richtige Ratsmitglied zurückgegeben wird. Bei meiner Krone, dafür muss er mir eine Gegenleistung erbringen. %s"
   princess_princess_delivered_rsc = "Ihr wart immer ein loyaler Anhänger."
   princess_neutral_delivered_rsc = "Aufgrund Deiner guten Gedanken bitte ich Dich darum, meine Fraktion zu unterstützen. Schreibe Deinen Namen auf diese Rolle hier..."
   princess_other_delivered_rsc = "Amen. Sogar die Diener der Thronanwärter beginnen, den besseren Weg zu erkennen. Sollen wir sie nicht zu unserer Fraktion überwechseln lassen?"
   princess_toopowered_rsc = "Im Vertrauen, wenn wir nicht bereits eine sehr starke Partei wären, würde ich Euch anbieten, Euch uns anzuschließen."

classvars:

   viQuestID = QST_ID_PRINCESS_SERVICE
   viGender = GENDER_FEMALE
   viFaction = FACTION_PRINCESS
   viKarma = 0
   viAttributes = MOB_NOFIGHT | MOB_LISTEN | MOB_RANDOM | MOB_RECEIVE | MOB_NOMOVE | MOB_PERM_QUESTER | MOB_TEACHER
   viOccupation = MOB_ROLE_ROYALTY

   viHomeroom = RID_CASTLE2C
   viMyRow = 33
   viMyCol = 23
   viMyAngle = ANGLE_NORTH
   viMyFR = 16
   viMyFC = 16

properties:

   vrName = princess_name_rsc
   vrIcon = princess_icon_rsc
   vrDesc = princess_desc_rsc


messages:

   AffectMood( why = $, what = $ )
   {
      if( why = MOODMOD_FACTION_CHANGE )
      {
	 if( what = FACTION_DUKE )
	 {
	    send( self, @SetMood, #new_mood = piMood - 50 );
	 }
	 if( what = FACTION_PRINCESS )
	 {
	    send( self, @SetMood, #new_mood = piMood + 50 );
	 }
      }
      return;
   }

   Constructor()
   {
      plWantedItems=[&Token];
      propagate;
   }

   CheckWhyWanted(obj=$,who=$)
   {
      local temp_rsc;
      if isClass(obj,&Token) and Send(SYS,@GetTokenGame) <> $ and 
         Send(Send(SYS,@GetTokenGame),@IsTokenReal,#obj=obj)
      {
         if Send(who,@GetFaction)=FACTION_PRINCESS
           {temp_rsc=princess_princess_delivered_rsc;}
         if Send(who,@GetFaction)=FACTION_NEUTRAL
           {temp_rsc=princess_neutral_delivered_rsc;}
         if Send(who,@GetFaction)=FACTION_DUKE
           {temp_rsc=princess_other_delivered_rsc;}
         if (Send(Send(SYS,@GetParliament),@GetPower,
               #faction=FACTION_PRINCESS)=FACTION_STRONGLY_IN) AND
           (Send(who,@GetFaction)<>FACTION_PRINCESS)
           { temp_rsc=princess_toopowered_rsc;} 

         Send(poOwner,@SomeoneSaid,#what=self,#type=SAY_RESOURCE,
                 #string=princess_token_delivered_rsc,#parm1=temp_rsc);
      return True;
      }
      propagate;
   }

   SetForSale()
   {
      plFor_Sale = [$,
                    $,
                    [ SID_CONCILIATION] ];
                    
      return;
   }

end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

