% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Wand is SpellItem

constants:

   include blakston.khd

resources:

   Wand_name_rsc = "Zauberstab"
   Wand_label_name_rsc = "Allgemeiner Zauberstab"
   Wand_icon_rsc = wand6.bgf
   Wand_desc_rsc = "Magische Energien umströmen diesen dünnen Stab."
   Wand_broken = "%s%s zerbricht in Stücke!"
   Wand_useless = "Der Stab hat seine magische Energie verbraucht und ist jetzt verkohlt und nutzlos."
   Wand_desc_broken_rsc = "Dieser einst so prächtige Stab ist jetzt nur noch ein wertloses Stück Holz."

   Wand_target_gone = "Du zielst mit Deinem Zauberstab sorgfältig auf %s%s, aber plötzlich merkst Du, dass Dein Ziel verschwunden ist."
   Wand_fails = "Du deutest mit Deinen Zauberstab aber nichts passiert. "

classvars:

   vrLabelName = Wand_label_name_rsc
   vrIcon = Wand_icon_rsc
   vrBroken =    Wand_broken
   vrFails = Wand_fails
   viSpellEffect = SID_FIREBALL
   viColor = XLAT_TO_RED
   viItem_Type = ITEMTYPE_WAND

   viGround_group = 2
   viInventory_group = 1

   vbNoBrokenView = TRUE

   viHits_init_min = 4
   viHits_init_max = 9

properties:

   vrName = Wand_name_rsc
   vrDesc = Wand_desc_rsc

   % Set to negative so it doesn't expire naturally.
   piGoBadTime = -1

messages:

   CastSpell(what = $, apply_on = $)
   {
      % If our owner and the target share the same owner (a room), then there needs to
      %  be line of sight.  Also, make sure the target is within sight and range.
      if (NOT ((send(poOwner,@GetOwner) <> send(apply_on,@GetOwner))
               OR send(send(poOwner,@GetOwner),@LineOfSight,#obj1=poOwner,#obj2=apply_on)))
         OR (IsClass(poOwner,&Player)
             AND NOT send(poOwner,@TargetWithinSightAndRange,#oTarget=apply_on))
      {
         send(self,@DoFailure,#what=what,#lTargets=[apply_on]);

         return;
      }

      propagate;
   }

   DoFailure(what = $,lTargets=$)
   {
      Send(what,@MsgSendUser,#message_rsc=vrFails,#parm1=send(first(ltargets),@GetinDef),
           #parm2=send(first(ltargets),@GetName));

      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

