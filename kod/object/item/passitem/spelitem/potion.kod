% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
Potion is SpellItem

constants:

   include blakston.khd

   % * 10^-4 hp / sec (half that of a spider bite)
   POISON_LOSSRATE = 1250
   % In ms.
   POISON_DURATION = 60000 

resources:

   Potion_name_rsc = "Trank"
   Potion_label_name_rsc = "allgemeiner Trank"
   Potion_icon_rsc = potion01.bgf
   Made_Potion_icon_rsc = potion02.bgf
   Potion_desc_rsc = "Dies ist eine Glasphiole mit einer farbigen Flüssigkeit."
   Potion_bad_desc_rsc = "Dies ist eine Glasphiole mit einer leicht trüben Flüssigkeit. Nur ein Hauch davon genügt, um Dir den Magen umzudrehen."



   Potion_broken = "Du stürzt den Inhalt der Phiole in einem einzigen Zug herunter."
   Potion_bad_drink = "Du stürzt den faulig schmeckenden Inhalt der Phiole herunter bemerkst fast umgehend, dass das eine ziemlich schlechte Idee war. Mit der Zeit ist er sauer geworden."



   Potion_sell_info = "Nimm diesen Trank bald, Schätzchen, denn er wird schnell schlecht."


   Potion_has_karma_requirement = "Bevor du dies trinkst, stell sicher das du ausreichend Karma besitzt um den darin enthaltenen Zauber auszuführen."


   Potion_gulp_sound = drkptn.wav

classvars:

   vrLabelName = Potion_label_name_rsc
   vrBadDesc = Potion_bad_desc_rsc
   vrDesc = Potion_desc_rsc
   vrBroken = Potion_Broken
   vrKarmaDesc = Potion_has_karma_requirement

   viBulk = 20
   viWeight = 17
   viValue_average = 60

   viFilling = 10

   viObject_flags = GETTABLE_YES
   viItem_type = ITEMTYPE_POTION | ITEMTYPE_SUNDRY
   viUse_type = ITEM_SINGLE_USE

   viPoisonPercent = 0
   viGoBadDamage = 0
   vbNoBrokenView = TRUE

   viGround_Group = 1

   viHits_init_min = 1
   viHits_init_max = 1

properties:

   vrName = Potion_name_rsc
   vrIcon = Potion_icon_rsc
   vrBroken = Potion_broken

   piMinSpellPower = 20
   piMaxSpellPower = 60

messages:

   Constructor(maker=$)
   {
      if maker <> $
      {
         vrIcon = Made_Potion_icon_rsc;
      }

      propagate;
   }

   ReqNewApply(what = $,apply_on = $)
   {
      if NOT Send(apply_on,@ReqEatSomething,#filling=viFilling)
      {
	      return FALSE;
      }

      if what <> apply_on
      {
         return FALSE;
      }

      propagate;
   }
   
   NewApplied(what = $,apply_on = $)
   {
      local oSpell;

      if isClass(apply_on,&User) 
      {
         send(apply_on,@WaveSendUser,#wave_rsc=Potion_gulp_sound);
      }

      if pbBad
      {
         Send(apply_on,@MsgSendUser,#message_rsc=Potion_bad_drink);
         % do some damage!
         if send(apply_on,@AssessDamage,#report=FALSE,#what=what,
                 #damage=(viGoBadDamage*Random(8,12)/10),
                 #aspell=ATCK_SPELL_ACID) = $
         {
            Send(what,@KilledSomething,#what=apply_on);
         }

         % maybe poison, too!
         if Random(1,100) <= viPoisonPercent
         {
            oSpell = Send(SYS,@FindSpellByNum,#num=SID_POISON);
            Send(oSpell,@MakePoisoned,#who=apply_on,
                 #lossrate=(POISON_LOSSRATE*Random(8,12)/10),
                 #duration=(POISON_DURATION*Random(8,12)/10));
         }

         send(self,@Delete);

         return;
      }

      Send(apply_on,@EatSomething,#nutrition=0,#filling=viFilling);

      propagate;
   }

   IsBeverage()
   {
      return TRUE;
   }

   DoFailure(what = $)
   {
      % Overriding because the superclass gives a "broken" message.  Oops.
      return;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

