% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
StaffOfJolting is SpecialWand

% TODO: This probably should be cleaned up so that it doesn't do everything in the Req message.

constants:

   include blakston.khd

   % Number of milliseconds delay before user can do anything after use.
   STAFF_DELAY = 3000

resources:

   staffofjolting_name_rsc = "Das Wurzelholz"
   staffofjolting_icon_rsc = joltstaf.bgf
   staffofjolting_desc_rsc = "Du schaust auf dieses - lieber nicht näher zu beschreibende - Stück Holz und siehst keinen Grund, wieso jemand an diesem kläglichen Gegenstand Interesse haben sollte."



   staffofjolting_jolt = "Du klopfst mit dem Wurzelholz dreimal auf den Boden."
   staffofjolting_jolt_third = "%s%s tippt mit einem knorrigen Stock dreimal auf dem Boden."

   staffofjolting_broke_concentration = "%s%s verliert die Konzentration und schaut verwirrt um sich."

   staffofjolting_used = "Du klopfst mit dem Wurzelholz auf den Boden, aber nichts passiert."

   staffofjolting_sound = woodstaf.wav

classvars:

   vrName = staffofjolting_name_rsc
   vrIcon = staffofjolting_icon_rsc

   viBulk = 40
   viWeight = 40
   viValue_average = 200

   viHits_init_min = 25
   viHits_init_max = 35

   viInventory_group = 2

   viObject_flags = GETTABLE_YES

properties:
   
   vrDesc = staffofjolting_desc_rsc

messages:

   %% when used, the staff of jolting breaks any trances in the room.

   ReqNewApply()
   {
      local oRoom, i, j, oWatcher, bArena;

      oRoom = Send(poOwner, @GetOwner);

      if piHits < 1
      {
         send(poOwner,@MsgSendUser,#message_rsc=staffofjolting_used);
         
         return FALSE;
      }

      if NOT send(poOwner,@IsOkayAttackTime,#time=STAFF_DELAY)
      {
         return FALSE;
      }

      piHits = piHits - 1;

      Send(oRoom,@SomethingWaveRoom,#what=poOwner,#wave_rsc=staffofjolting_sound);

      for i in send(oRoom,@GetHolderActive)
      {
         if NOT isClass(first(i),&Player)
         {
            continue;
         }

         if first(i) = poOwner
         {
            send(first(i),@MsgSendUser,#message_rsc=staffofjolting_jolt);
         }
         else
         {
            if NOT send(oRoom,@IsArena)
            {
               send(first(i),@MsgSendUser,#message_rsc=staffofjolting_jolt_third,
                    #parm1=send(poOwner,@GetCapIndef),#parm2=send(poOwner,@GetName));
            }
            else
            {
               if send(oRoom,@InPlay,#what=first(i))
               {
                  send(first(i),@MsgSendUser,#message_rsc=staffofjolting_jolt_third,
                       #parm1=send(poOwner,@GetCapIndef),#parm2=send(poOwner,@GetName));
               }
            }
         }
      }

      if send(oRoom,@CheckRoomFlag,#FLAG=ROOM_NO_COMBAT)
         OR send(oRoom,@CheckRoomFlag,#FLAG=ROOM_NO_PK)
      {
         return FALSE;
      }
      
      bArena = FALSE;

      if send(oRoom,@IsArena)
      {
         bArena = TRUE;
         oWatcher = send(oRoom,@GetWatcher);
         If oWatcher = $
         {
            DEBUG("Arena without watcher!");
         }
         
         if NOT send(oWatcher,@IsCombatant,#who=poOwner)
         {
            return FALSE;
         }
      }
      else
      {
         if NOT send(poOwner,@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE)
         {
            return FALSE;
         }
      }

      for i in send(oRoom,@GetHolderActive)
      {
         if NOT isClass(first(i),&Player)
            OR (NOT send(first(i),@CheckPlayerFlag,#flag=PFLAG_PKILL_ENABLE)
                AND NOT bArena)
         {
            continue;
         }

         if random(1,8) = 1
         {
            continue;
         }

         if send(first(i),@BreakTrance,#event=EVENT_DISRUPT)
         {
            for j in send(oRoom,@GetHolderActive)
            {
               if first(j) <> first(i)
               {
                  send(first(j),@MsgSendUser,#message_rsc=staffofjolting_broke_concentration,
                       #parm1=send(first(i),@GetCapIndef),#parm2=send(first(i),@GetName),
                       #parm3=send(first(i),@GetHeShe),#parm4=send(first(i),@GetHisHer));
               }
            }
         }
      }
      
      return FALSE;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

