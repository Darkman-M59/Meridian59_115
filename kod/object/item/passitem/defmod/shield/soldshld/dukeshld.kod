% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
DukeShield is SoldierShield

% Stats affected: Intellect and Stamina
% Special bonus: This shield gives a bonus 20 Spellpower to all spells.

constants:

   include blakston.khd

resources:

   DukeShield_name_rsc = "Ein Schild der Armee des Herzogs"

   DukeShield_desc_rsc = "Dieser Schild weist dich als Elitesoldat der Armee des Herzogs aus."


   DukeShield_ArmyName = "der herzöglichen Armee"

   DukeShield_emblem_over = gshdaov.bgf
   DukeShield_emblem_drop = gshda.bgf
   DukeShield_emblem_back = gshdabk.bgf

   DukeShield_welcome_mail = "Betreff: Du bist nun ein Elitesoldat.\n\nWillkommen in der Armee des wahren Führers dieser Lande. Dein Ehrgeiz steht der sehr gut.\n\n\nDies sind einige Regeln von denjenigen, die sich über uns wähnen. Es passt zu unseren Zielen, diesen Regeln zu folgen.\n\n\nFRAKTIONSSCHILD: Dein Schild weist dich als Mitglied meiner Armee aus. Solltest du diesen Schild verlieren zeigt das an, dass du nicht länger ein Teil unserer Armee bist.\n\nFEINDLICHE SOLDATEN: Feinde von anderen erbarmungswürdigen Fraktionen haben das volle Recht, dich anzugreifen, sollten sie dich aufgreifen. Sie werden dafür keine Bestrafung erhalten. Natürlich wirst du ebenfalls nicht bestraft werden, wenn du sie im Kampf vernichtest. Ich bin sicher, dass unsere feigen Feinde an sicheren Orten wie Kneipen oder Gemeinschaftshallen Schutz suchen werden, aber es ist deine Aufgabe, darauf zu warten dass sie aus ihren Verstecken hervorkommen. Wie bei den regulären Mitgliedern meiner Fraktion erlaube ich keine verräterischen Angriffe gegen die, die mir loyal sind; solltest du dies dennoch tun, wirst du damit deine eigene Stärke reduzieren, wirst du doch in allen Belangen aus meinem Dienst entfernt werden. Um unsere Feinde besser bekämpfen zu können lehre ich dich, unseren Feinden zusätzlichen Schaden beizubringen. Tode, unseren Feinden zugefügt, werden ihnen die Strafe bescheren, die sie verdienen, indem sie sich gegen uns stellen!\n\nRÄNGE: Töte unsere Feinde, und ich werde dich erhoben sehen, um von denen, die mir folgen angebetet zu werden. Sterbe, oder verhalte dich unehrenhaft, und du wirst dich am untersten Ende der Armee wiederfinden. Diejeningen, die versuchen das Land unsicher zu verlassen und einen Geist zurückzulassen um dem Kampf zu entgehen, wird mein Verdruss ebenfalls treffen.\n\nFÄHIGKEITEN: Als Gegenleistung für deinen loyalen Dienst gebe ich dir Kräfte! Ich lehre dich, wie du deine natürliche Konstitution und Gewandtheit im Kampf erhöhen kannst. Ich werde dir ebenfalls einen Einblick in die dunkleren Aspekte der Magie geben, um all deine magischen Sprüche zu verstärken. Diese Kräfte steigen, wenn du meine Aufmerksamkeit erhältst und in den Rängen der Armee aufsteigst.\n\nDIE RATSMITGLIEDER: Die Ratsmitglieder, die wir zu überzeugen suchen wünschen keine Soldaten, die in die Angelegenheiten der Artefakte eingreifen. Für den Zeitpunkt: Tu was sie sagen, denn wir müssen ihr Vertrauen gewinnen, wenn sie mich als den wahren Herrscher deklarieren sollen, der ich bin. Lasse einen meiner Anhänger, der kein Soldat ist das Artefakt nehmen und die Ratsmitglieder von unserer Macht und unserem Recht überzeugen. Deine Aufgabe ist es jetzt, Territorien in meinem Namen zu besetzen. Nur Soldaten können Gebiete für unsere Fraktion erobern.\n\nLOYALITÄT: Befasse dich nicht mit steuerlichen Botengängen, solange du ein Soldat bist. Ermorde unsere Feinde, um mir all die Loyalität zu zeigen, die ich brauche.\n\n\nWir werden den Thron dieses Königreiches besteigen. Du hast deine Unterstützung weise gewählt.\n\n\nAkardius, Herzog auf Burg Schwarzstein"



































classvars:

   vrName = DukeShield_name_rsc
   vrDesc = DukeShield_desc_rsc

   prShield_emblem_held = DukeShield_emblem_over
   prShield_emblem_drop = DukeShield_emblem_drop
   prShield_emblem_back = DukeShield_emblem_back

   viFaction = FACTION_DUKE
   viColor = XLAT_TO_PURPLE

   % Stuff for rank
   vrArmyName = DukeShield_ArmyName

   vrWelcomeMessage = DukeShield_welcome_mail

properties:

   piStaminaChange = 0
   piAgilityChange = 0

messages:

   RemoveStats()
   {
      if poOwner <> $
         AND IsClass(poOwner,&Player)
      {
         % Reset owner's stats
         send(poOwner,@AddStamina,#points=-piStaminaChange);
         send(poOwner,@AddAgility,#points=-piAgilityChange);
         piStaminaChange = 0;
         piAgilityChange = 0;
      }

      return;
   }

   UpdateRankPowers()
   "Updates any rank powers.  Affects stats."
   {
      local iStatAdjustment, iNewAdjustment;

      if poOwner = $
         OR NOT IsClass(poOwner,&Player)
      {
         return;
      }

      if piFactionRank <= 2
      {
         iStatAdjustment = 0;
      }
      else
      {
         if piFactionRank <= 6
         {
            iStatAdjustment = 3;
         }
         else
         {
            iStatAdjustment = 5;
         }
      }

      % In the following:
      %  iNewAdjustment is how much the stat changed this time around.
      %  pi(stat)Change is the cumulative change to that stat.
      if iStatAdjustment <> piAgilityChange
      {
         iNewAdjustment = send(poOwner,@AddAgility,#points=(iStatAdjustment-piAgilityChange));
         piAgilityChange = piAgilityChange + iNewAdjustment;
      }

      if iStatAdjustment <> piStaminaChange
      {
         iNewAdjustment = send(poOwner,@AddStamina,#points=(iStatAdjustment-piStaminaChange));
         piStaminaChange = piStaminaChange + iNewAdjustment;
      }

      return;
   }

   GetBaseSpellModifier(oSpell=$)
   {
      local iModifier;

      iModifier = ((15 * piFactionRank) / 10);

      return iModifier;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

