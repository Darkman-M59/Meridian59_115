% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
RebelShield is SoldierShield

% Stats affected: Might and Aim
% Special bonus: This shield protects you from 2 more points of damage than normal.

constants:

   include blakston.khd

resources:

   RebelShield_name_rsc = "Ein Schild der Rebellenmiliz"

   RebelShield_desc_rsc = "Dieser Schild weist dich als Soldat der Rebellenmiliz unter Jonas D'Accor aus."


   RebelShield_ArmyName = "der Rebellenmiliz"

   RebelShield_emblem_over = gshreov.bgf
   RebelShield_emblem_drop = gshre.bgf
   RebelShield_emblem_back = gshrebk.bgf

   RebelShield_welcome_mail = "Betreff: Willkommen, Freiheitskämpfer.\n\nIch bin froh zu sehen, dass du aktiver in unserem Kampf für die Zukunft unseres Landes und gegen die Tyrannei werden willst.\n\n\nIch erwarte ein gewisses Maß an Disziplin von den Soldaten meiner Miliz. Lies diese Regeln und folge ihnen ohne Fragen.\n\n\nDER FRAKTIONSSCHILD: Dein Schild ist das Zeichen, dass du ein 'offizieller' Teil unserer Rebellenmiliz bist. Du musst es immer tragen, um deinen Status innerhalb der Rebellenarmee zu demonstrieren.\n\nFEINDLICHE SOLDATEN: Sei gewarnt! Du kannst von denen, die die Farben unserer Fraktionsgegner tragen ohne Strafe angegriffen werden. Nur die Sicherheit von Kneipen oder Hallen kann dich vor Angriffen schützen. Angriffe gegen Feinde bleiben ohne Bestrafung für dich, so wie deine Feinde nicht bestraft werden, wenn sie dich angreifen. Einen Soldaten, der mit unserer Fraktion verbündet ist anzugreifen, ist genug Grund dich unehrenhaft aus unserer Armee und der Fraktion zu entlassen. Ich werde dich verschiedene Wege lehren, mehr Schaden anzurichten, um dich gegen Feinde zu verteidigen und uns zu helfen, unseren Platz in der Regierung dieses Landes wiederzugewinnen. Du solltest wissen, dass jeder Tod durch die Hand eines Feindes dir keineswegs erleichtert wird.\n\nRÄNGE: Deine Position innerhalb unserer Streitkräfte wird durch deine Taten festgelegt. Siege über feindliche Soldaten wird dich im Rang aufsteigen lassen. Wenn du getötet wirst, oder wenn du dem Kampf fern bleibst wirst du absteigen. Auch wird deine Flucht vor dem Kampf mittels Verlassen dieser Welt in unsicheren Gebieten dich mit meiner Missgunst in Konfrontation bringen.\n\nFÄHIGKEITEN: Dir wird als Soldat zusätzliches Training zuteil werden. Ich werde deine natürliche Stärke und Genauigkeit durch hartes Training erhöhen. Darüber hinaus werde ich dir darüber beibringen, wie du den Schaden durch gegnerische Schläge vermindern kannst. Natürlich werde ich mich mit dem Training der höheren Ränge mehr beschäftigen als mit dem der niedrigeren.\n\nDIE RATSMITGLIEDER: Dein Schild stört deine Fähigkeit, Artefakte zu bergen. Ich möchte, dass du einen Anhänger der Rebellen suchst, der das Artefakt zum richtigen Ratsmitglied zurückbringt, solltest du eines finden. Beachte auch, dass nur richtige Soldaten mit einem Schild Gebiete beanspruchen können. Die Flaggen in Pietros Kneipe dürfen von nun an nur von Soldaten getragen und benutzt werden.\n\nLOYALITÄT: Ein Soldat wie du es bist sollte sich vorrangig um seine eigene Ausrüstung kümmern. Daher erwarte ich nicht von dir, unsere Wachen an den Flaggenmasten mit Ausrüstung zu versorgen, solange du den Rang eines Soldaten inne hast.\n\n\nDein Waffenbruder,\nJonas D'Accor"

































classvars:

   vrName = RebelShield_name_rsc
   vrDesc = RebelShield_desc_rsc

   prShield_emblem_held = RebelShield_emblem_over
   prShield_emblem_drop = RebelShield_emblem_drop
   prShield_emblem_back = RebelShield_emblem_back

   viDamage_base = 2                % This is the default value for piDamage_reduce

   viFaction = FACTION_REBEL
   viColor = XLAT_TO_RED

   % Stuff for rank
   vrArmyName = RebelShield_ArmyName

   vrWelcomeMessage = RebelShield_welcome_mail

properties:

   piMightChange = 0
   piAimChange = 0

messages:

   RemoveStats()
   {
      if poOwner <> $
         AND IsClass(poOwner,&Player)
      {
         % Reset owner's stats
         send(poOwner,@AddMight,#points=-piMightChange);
         send(poOwner,@AddAim,#points=-piAimChange);
         piMightChange = 0;
         piAimChange = 0;
      }

      return;
   }

   UpdateRankPowers()
   "Updates any rank powers.  Affects stats."
   {
      local iStatAdjustment, iNewAdjustment;

      if poOwner = $
         OR NOT IsClass(poOwner,&Player)
      {
         return;
      }

      if piFactionRank <= 2
      {
         iStatAdjustment = 0;
      }
      else
      {
         if piFactionRank <= 6
         {
            iStatAdjustment = 3;
         }
         else
         {
            iStatAdjustment = 5;
         }
      }

      % In the following:
      %  iNewAdjustment is how much the stat changed this time around.
      %  pi(stat)Change is the cumulative change to that stat.
      if iStatAdjustment <> piMightChange
      {
         iNewAdjustment = send(poOwner,@AddMight,#points=(iStatAdjustment-piMightChange));
         piMightChange = piMightChange + iNewAdjustment;
      }

      if iStatAdjustment <> piAimChange
      {
         iNewAdjustment = send(poOwner,@AddAim,#points=(iStatAdjustment-piAimChange));
         piAimChange = piAimChange + iNewAdjustment;
      }

      return;
   }

   ModifyDefenseDamage(who = $,what = $,damage = $,atype = 0,aspell = 0)
   "Sets piDamage_reduce for damage reduction in the superclass."
   {
      % This ranges from 2-4.  At low end, we absorb 0-2 damage.  At high end, 1-4.
      piDamage_reduce = viDamage_base + ((piFactionRank/3) - 1);

      propagate;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

