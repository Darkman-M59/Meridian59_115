% Meridian 59, Copyright 1994-2012 Andrew Kirmse and Chris Kirmse.
% All rights reserved.
%
% This software is distributed under a license that is described in
% the LICENSE file that accompanies it.
%
% Meridian is a registered trademark.


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
PrincessShield is SoldierShield

% Stats affected: Mysticism and Agility
% Special bonus: Using a Princess shield makes your spells cost up to 2 less mana.

constants:

   include blakston.khd

resources:

   PrincessShield_name_rsc = "Ein Schild der Armee der Prinzessin."

   PrincessShield_desc_rsc = "Dieser Schild identifiziert dich als stolzes Mitglied der Armee der Prinzessin."


   PrincessShield_ArmyName = "der königlichen Armee"

   PrincessShield_emblem_over = gshprov.bgf
   PrincessShield_emblem_drop = gshpr.bgf
   PrincessShield_emblem_back = gshprbk.bgf

   PrincessShield_welcome_mail = "Betreff: Willkommen in der königlichen Armee.\n\nGrüße. Ich heiße dich als neuen Verbündeten in unserem Kampf, den Thron gegen Mächte, die uns schaden wollen zu verteidigen willkommen.\n\nEs gibt einige Regeln, die du in deiner Rolle als Soldat einhalten musst.\n\n\nDER FRAKTIONSSCHILD: Dein Schild ist das Zeichen, dass du offiziell ein Teil der königlichen Armee bist. Du musst es immer tragen, um deine Position zu demonstrieren.\n\nFEINDLICHE SOLDATEN: Du hast dich damit einverstanden erklärt, dass du jederzeit von gegnerischen Soldten in unsicheren Gebieten angegriffen werden kannst. Dein Angreifer wird für solch einen Angriff, selbst wenn er dich tötet nicht als Krimineller bestraft werden. Genauso kannst du Feinde angreifen, ohne eine Strafe dafür befürchten zu müssen. Beachte, dass ein Angriff auf unsere eigenen Soldaten mich sehr ärgern würde. In solch einem Fall werde ich dich sowohl als Soldat als auch als Mitglied unserer Fraktion entlassen. Unsere Rechtschaffenheit birgt für dich die Möglichkeit, mit jedem Angriff feindlichen Soldaten zusätzlichen Schaden zuzufügen. Denke auch daran, dass der Tod durch die Hand feindlicher Soldaten echt ist, du wirst die üblichen Strafen erleiden müssen.\n\nRÄNGE: Deine Position innerhalb der Armee wird durch deine Handlungen bestimmt. Das Töten von feindlichen Soldaten erlaubt deinen Aufstieg in den Rängen der Armee. Wenn du getötet wirst oder wenn du für eine längere Zeit inaktiv bleibst wirst du einen Abstieg in den Rängen erfahren. Versuche nicht, Kämpfen zu entgehen indem du diese Welt in unsicheren Gebieten verlässt, denn das wird deinen Rang ebefalls vermindern.\n\nFÄHIGKEITEN: Mit deinem Status als Fraktionssoldat werde ich dir zusätzliche Wege aufzeigen, deine Macht zu vergrößern. Ich werde dich intelligenter machen und dich in die Lage versetzen, alle mystischen Dinge zu verstehen. Ich werde dich auch Wege lehren, die Kosten deiner magischen Sprüche zu reduzieren. Das Potenzial dieser Fähigkeiten steigt mit deinem Rang.\n\nDIE RATSMITGLIEDER: Die Ratsmitlgieder haben festgelegt, dass die Artefakte zu sensibel sind, um sie den Händen von Soldaten anzuvertrauen. Daher wirst du instruiert, keine Artefakte aufzuheben, die du finden könntest. Ich ermutige dich dazu, einen Verbündeten zu suchen, der das Artefakt zum richtigen Ratsmitglied bringt, solltest du eines finden. Zusätzlich haben die Ratsmitglieder festgesetzt, dass von nun an nur noch Soldaten Gebiete erobern dürfen. Die Flaggen in meinem Saal dürfen nur von anerkannten Soldaten meiner Fraktion getragen werden.\n\nLOYALITÄT: Solange du der königlichen Armee dienst, werde ich dich nicht dazu auffordern, mir deine Loyalität zu beweisen, wie ich es sonst tue. Du solltest dich darauf konzentrieren deinen Rang zu erhalten.\n\n\nMöge Sha Lill unsere Taten führen und unseren Sieg sichern!\n\n\nGezeichnet, Prinzessin Kateriina, Regentin Meridians."


































classvars:

   vrName = PrincessShield_name_rsc
   vrDesc = PrincessShield_desc_rsc

   prShield_emblem_held = PrincessShield_emblem_over
   prShield_emblem_drop = PrincessShield_emblem_drop
   prShield_emblem_back = PrincessShield_emblem_back

   viFaction = FACTION_PRINCESS
   viColor = XLAT_TO_ORANGE

   % Stuff for rank
   vrArmyName = PrincessShield_ArmyName

   vrWelcomeMessage = PrincessShield_welcome_mail

properties:

   piIntellectChange = 0
   piMysticismChange = 0

messages:

   RemoveStats()
   {
      if poOwner <> $
         AND IsClass(poOwner,&Player)
      {
         % Reset owner's stats
         send(poOwner,@AddIntellect,#points=-piIntellectChange);
         send(poOwner,@AddMysticism,#points=-piMysticismChange);
         piIntellectChange = 0;
         piMysticismChange = 0;
      }

      return;
   }

   UpdateRankPowers()
   "Updates any rank powers.  Affects stats."
   {
      local iStatAdjustment, iNewAdjustment;

      if poOwner = $
         OR NOT IsClass(poOwner,&Player)
      {
         return;
      }

      if piFactionRank <= 2
      {
         iStatAdjustment = 0;
      }
      else
      {
         if piFactionRank <= 6
         {
            iStatAdjustment = 3;
         }
         else
         {
            iStatAdjustment = 5;
         }
      }

      % In the following:
      %  iNewAdjustment is how much the stat changed this time around.
      %  pi(stat)Change is the cumulative change to that stat.
      if iStatAdjustment <> piIntellectChange
      {
         iNewAdjustment = send(poOwner,@AddIntellect,#points=(iStatAdjustment-piIntellectChange));
         piIntellectChange = piIntellectChange + iNewAdjustment;
      }

      if iStatAdjustment <> piMysticismChange
      {
         iNewAdjustment = send(poOwner,@AddMysticism,#points=(iStatAdjustment-piMysticismChange));
         piMysticismChange = piMysticismChange + iNewAdjustment;
      }

      return;
   }

   GetSpellManaReduction()
   {
      local iReduction;

      iReduction = ((piFactionRank/3) - 1);

      return iReduction;
   }


end
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

